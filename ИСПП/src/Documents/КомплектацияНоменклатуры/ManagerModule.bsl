#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Имена реквизитов, от значений которых зависят параметры учета номенклатуры
//
// Возвращаемое значение:
//   Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУчетаНоменклатуры() Экспорт
	
	Возврат "Склад";
	
КонецФункции

// Возвращает параметры учета для номенклатуры, указанной в документе
//
// Параметры
//   Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
// Возвращаемое значение
//   Структура - Состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУчетаНоменклатуры
//
Функция ПараметрыУчетаНоменклатуры(Объект) Экспорт
	
	ПараметрыУчетаНаСкладе = СкладыСервер.ПараметрыУчетаНоменклатуры(Объект.Склад);
	
	ПараметрыУчетаШапка = ЗапасыСервер.ПараметрыУчетаНоменклатуры();
	ПараметрыУчетаШапка.ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыУчетаШапка.ТоварВШапке = Истина;
	ПараметрыУчетаШапка.ИспользоватьСерии = ПараметрыУчетаНаСкладе.ИспользоватьСерииНоменклатуры;
	ПараметрыУчетаШапка.ИспользоватьПартии = ПараметрыУчетаНаСкладе.ИспользоватьПартии;
	ПараметрыУчетаШапка.Склад = Объект.Склад;
	
	ПараметрыУчетаТабличнаяЧасть = ЗапасыСервер.ПараметрыУчетаНоменклатуры();
	ПараметрыУчетаТабличнаяЧасть.ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыУчетаТабличнаяЧасть.ИспользоватьСерии = ПараметрыУчетаНаСкладе.ИспользоватьСерииНоменклатуры;
	ПараметрыУчетаТабличнаяЧасть.ИспользоватьПартии = ПараметрыУчетаНаСкладе.ИспользоватьПартии;
	ПараметрыУчетаТабличнаяЧасть.Склад = Объект.Склад;
	
	ПараметрыУчета = Новый Структура;
	ПараметрыУчета.Вставить("Шапка", ПараметрыУчетаШапка);
	ПараметрыУчета.Вставить("ТабличнаяЧасть", ПараметрыУчетаТабличнаяЧасть);
	
	Возврат ПараметрыУчета;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания параметров учета номенклатуры
//
// Параметры
//   ПараметрыУчетаНоменклатуры - Структура - состав полей задается в функции ЗапасыСервер.ПараметрыУчетаНоменклатуры
//
// Возвращаемое значение
//   Строка - текст запроса
//
Функция ТекстЗапросаРасчетаСтатусовУчетаНоменклатуры(ПараметрыУчетаНоменклатуры) Экспорт
	
	Возврат ЗапасыСервер.ТекстЗапросаРасчетаСтатусовУчетаНоменклатуры(ПараметрыУчетаНоменклатуры);
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение
#Область Проведение

// Инициализирует таблицы значений, содержащие данные для проведения документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицыДвиженийДляПроведения(ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ОсновныеДанныеДокумента = ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства);
	
	ИнициализироватьПартииНоменклатурыВДокументе(ОсновныеДанныеДокумента);
	ИнициализироватьКлючиАналитикиВидаУчета(ОсновныеДанныеДокумента);
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(ОсновныеДанныеДокумента);
	
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтТаблицаТовары());
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтТаблицаКомплект());
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаТоварыНаСкладах()                 , Метаданные.РегистрыНакопления.ТоварыНаСкладах);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаСвободныеОстатки()                , Метаданные.РегистрыНакопления.СвободныеОстатки);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтАналитика());
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтАналитикаУчетныеЦены());
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаСебестоимостьТоваров()            , Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	Запрос = Новый Запрос(ПроведениеПоддержкаПроектов.ПолучитьТекстЗапросаДвижений(ДополнительныеСвойства, Регистры));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Для Каждого ДанныеДокумента Из ОсновныеДанныеДокумента Цикл
		Запрос.УстановитьПараметр(ДанныеДокумента.Ключ, ДанныеДокумента.Значение);
	КонецЦикла;
	
	ПроведениеПоддержкаПроектов.ЗаполнитьТаблицыДвижений(ДополнительныеСвойства, Запрос.ВыполнитьПакет(), Регистры);
	
КонецПроцедуры

Функция ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства)
	
	ЗапрашиваемыеДанные = Новый Структура;
	ЗапрашиваемыеДанные.Вставить("Ссылка");
	ЗапрашиваемыеДанные.Вставить("Период", "Дата");
	ЗапрашиваемыеДанные.Вставить("Организация");
	ЗапрашиваемыеДанные.Вставить("Склад");
	ЗапрашиваемыеДанные.Вставить("ХозяйственнаяОперация");
	ЗапрашиваемыеДанные.Вставить("ВидОперации");
	
	ОсновныеДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПроведениеПоддержкаПроектов.ПолучитьСсылкуНаДокументДляПроведения(ДополнительныеСвойства),
		ЗапрашиваемыеДанные);
		
	ОсновныеДанныеДокумента.Вставить("Граница", Новый Граница(ОсновныеДанныеДокумента.Ссылка.МоментВремени(), ВидГраницы.Исключая));
	
	ЗапасыСервер.ПриПодготовкеОсновныхДанныхДляПроведения(ДополнительныеСвойства, ОсновныеДанныеДокумента);
	
	Возврат ОсновныеДанныеДокумента;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаТовары()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ВидОперации                                      КАК ВидОперации,
	|	ТаблицаТовары.НомерСтроки                         КАК НомерСтроки,
	|	&Организация                                      КАК Организация,
	|	ТаблицаТовары.Номенклатура                        КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетПоСериям, &СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                             КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетПоПартиям, &СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                             КАК Партия,
	|	ТаблицаТовары.Количество                          КАК Количество,
	|	ТаблицаТовары.ЕдиницаИзмерения                    КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                             КАК СерияНоменклатурыДляСебестоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                             КАК ПартияДляСебестоимости,
	|	ТаблицаТовары.Цена КАК Цена
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.КомплектацияНоменклатуры.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаКомплект()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ВидОперации                                      	КАК ВидОперации,
	|	1                                                   КАК НомерСтроки,
	|	&Организация                                        КАК Организация,
	|	&Склад                                              КАК Склад,
	|	ТаблицаТовары.Номенклатура                              КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетПоСериям, &СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                               КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетПоПартиям, &СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                               КАК Партия,
	|	ТаблицаТовары.Количество                            КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                               КАК СерияНоменклатурыДляСебестоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                КАК ПартияДляСебестоимости
	|ПОМЕСТИТЬ ВтТаблицаКомплект
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТоварыНаСкладах()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                           КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                   КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.Организация                   КАК Организация,
	|	&Склад			                            КАК Склад,
	|	ТаблицаТовары.Номенклатура                  КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры  			КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия             			КАК Партия,
	|	ТаблицаТовары.Количество                    КАК Количество
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
    |ГДЕ
    |	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
    |
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                                КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                        КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)           КАК ВидДвижения,
	|	&Период                                          КАК Период,
	|	ТаблицаТовары.Организация		             	 КАК Организация,
	|	&Склад                            			     КАК Склад,
	|	ТаблицаТовары.Номенклатура                       	 КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры        		 	 КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                   		 	 КАК Партия,
	|	ТаблицаТовары.Количество                     	 КАК Количество
	|ИЗ
	|	ВтТаблицаКомплект КАК ТаблицаТовары
    |ГДЕ
    |	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
    |
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3                                           КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                 	КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.Организация               	КАК Организация,
	|	&Склад			                            КАК Склад,
	|	ТаблицаТовары.Номенклатура				    	КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры	 	    	КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия				    	КАК Партия,
	|	ТаблицаТовары.Количество					КАК Количество
	|ИЗ
	|	ВтТаблицаКомплект КАК ТаблицаТовары
	|ГДЕ
    |	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
    |
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4                                                КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                        КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)           КАК ВидДвижения,
	|	&Период                                          КАК Период,
	|	ТаблицаТовары.Организация		                 КАК Организация,
	|	&Склад			                                 КАК Склад,
	|	ТаблицаТовары.Номенклатура                       КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры        			 КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                  			 КАК Партия,
	|	ТаблицаТовары.Количество                         КАК Количество
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
    |	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСвободныеОстатки()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	1                                           КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                   КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.Организация                   КАК Организация,
	|	&Склад			                            КАК Склад,
	|	ТаблицаТовары.Номенклатура                  КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры	 		    КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия		                КАК Партия,
	|	ТаблицаТовары.Количество                    КАК ВНаличии,
	|	0                                           КАК ВРезервеСоСклада
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                                КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                        КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)           КАК ВидДвижения,
	|	&Период                                          КАК Период,
	|	ТаблицаТовары.Организация              			 КАК Организация,
	|	&Склад			                                 КАК Склад,
	|	ТаблицаТовары.Номенклатура                       КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры			         КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия		                     КАК Партия,
	|	ТаблицаТовары.Количество                         КАК ВНаличии,
	|	0                                                КАК ВРезервеСоСклада
	|ИЗ
	|	ВтТаблицаКомплект КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3                                           КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                   КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.Организация                   КАК Организация,
	|	&Склад  		                            КАК Склад,
	|	ТаблицаТовары.Номенклатура                  КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры	            КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                        КАК Партия,
	|	ТаблицаТовары.Количество                    КАК ВНаличии,
	|	0                                           КАК ВРезервеСоСклада
	|ИЗ
	|	ВтТаблицаКомплект КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4                                                КАК Порядок,
	|	ТаблицаТовары.НомерСтроки                        КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)           КАК ВидДвижения,
	|	&Период                                          КАК Период,
	|	ТаблицаТовары.Организация			             КАК Организация,
	|	&Склад                                 			 КАК Склад,
	|	ТаблицаТовары.Номенклатура                       КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры			         КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия   		                     КАК Партия,
	|	ТаблицаТовары.Количество                         КАК ВНаличии,
	|	0                                                КАК ВРезервеСоСклада
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитика()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ТаблицаТовары.Номенклатура КАК Номенклатура,
	               |	ТаблицаТовары.СерияНоменклатурыДляСебестоимости КАК СерияНоменклатуры,
	               |	ТаблицаТовары.ПартияДляСебестоимости КАК Партия,
	               |	АналитикаУчетаНоменклатуры.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	               |	АналитикаВидаУчета.КлючАналитики КАК АналитикаВидаУчета
	               |ПОМЕСТИТЬ ВтАналитика
	               |ИЗ
	               |	ВтТаблицаТовары КАК ТаблицаТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	               |		ПО ТаблицаТовары.Номенклатура = АналитикаУчетаНоменклатуры.Номенклатура
	               |			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = АналитикаУчетаНоменклатуры.СерияНоменклатуры
	               |			И ТаблицаТовары.ПартияДляСебестоимости = АналитикаУчетаНоменклатуры.Партия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК АналитикаВидаУчета
	               |		ПО (АналитикаВидаУчета.Организация = ТаблицаТовары.Организация)
	               |			И (АналитикаВидаУчета.Склад = &Склад)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ТаблицаТовары.Номенклатура,
	               |	ТаблицаТовары.СерияНоменклатурыДляСебестоимости,
	               |	ТаблицаТовары.ПартияДляСебестоимости,
	               |	АналитикаУчетаНоменклатуры.КлючАналитики,
	               |	АналитикаВидаУчета.КлючАналитики
	               |ИЗ
	               |	ВтТаблицаКомплект КАК ТаблицаТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	               |		ПО ТаблицаТовары.Номенклатура = АналитикаУчетаНоменклатуры.Номенклатура
	               |			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = АналитикаУчетаНоменклатуры.СерияНоменклатуры
	               |			И ТаблицаТовары.ПартияДляСебестоимости = АналитикаУчетаНоменклатуры.Партия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК АналитикаВидаУчета
	               |		ПО (АналитикаВидаУчета.Организация = ТаблицаТовары.Организация)
	               |			И (АналитикаВидаУчета.Склад = &Склад)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАналитикаУчетныеЦены()
	
	ТекстЗапросаУчетныеЦены = 
	"ВЫБРАТЬ
	|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры			 						КАК АналитикаУчетаНоменклатуры,
	|	СебестоимостьТоваров.АналитикаВидаУчета					 						КАК АналитикаВидаУчета,
	|	ВЫБОР КОГДА СебестоимостьТоваров.КоличествоОстаток = 0
	|		ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СебестоимостьТоваров.СтоимостьОстаток КАК ЧИСЛО(20, 9)) / ВЫРАЗИТЬ(СебестоимостьТоваров.КоличествоОстаток КАК ЧИСЛО(20, 9))
	|	КОНЕЦ 																			КАК УчетнаяЦена,
	|	ВЫБОР КОГДА СебестоимостьТоваров.КоличествоОстаток = 0                                         
	|		ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СебестоимостьТоваров.СтоимостьБезНДСОстаток КАК ЧИСЛО(20, 9)) / ВЫРАЗИТЬ(СебестоимостьТоваров.КоличествоОстаток КАК ЧИСЛО(20, 9))
	|	КОНЕЦ 																			КАК УчетнаяЦенаБезНДС
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(
	|			&Граница,
	|			(АналитикаВидаУчета, АналитикаУчетаНоменклатуры) В
	|				(ВЫБРАТЬ
	|					Врем.АналитикаВидаУчета,
	|					Врем.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ВтАналитика КАК Врем)) КАК СебестоимостьТоваров";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Аналитика.Номенклатура								КАК Номенклатура,
	|	Аналитика.СерияНоменклатуры							КАК СерияНоменклатуры,
	|	Аналитика.Партия									КАК Партия,
	|	Аналитика.АналитикаУчетаНоменклатуры		 		КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.АналитикаВидаУчета		 				КАК АналитикаВидаУчета,
	|	ЕстьNULL(УчетныеЦеныНоменклатуры.УчетнаяЦена, 0)	КАК УчетнаяЦена,
	|	ЕстьNULL(УчетныеЦеныНоменклатуры.УчетнаяЦенаБезНДС, 0)	КАК УчетнаяЦенаБезНДС
	|ПОМЕСТИТЬ ВтАналитикаУчетныеЦены
	|ИЗ
	|	ВтАналитика КАК Аналитика
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			(" + ТекстЗапросаУчетныеЦены + ") КАК УчетныеЦеныНоменклатуры
	|		ПО 
	|			Аналитика.АналитикаУчетаНоменклатуры = УчетныеЦеныНоменклатуры.АналитикаУчетаНоменклатуры
	|			И Аналитика.АналитикаВидаУчета		 = УчетныеЦеныНоменклатуры.АналитикаВидаУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|   СерияНоменклатуры,
	|   Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСебестоимостьТоваров()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	1 КАК Порядок,
	               |	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	               |	&Период КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	Аналитика.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	               |	Аналитика.АналитикаВидаУчета КАК АналитикаВидаУчета,
	               |	ТаблицаТовары.Количество КАК Количество,
	               |	ТаблицаТовары.Количество * Аналитика.УчетнаяЦена КАК Стоимость,
	               |	ТаблицаТовары.Количество * Аналитика.УчетнаяЦенаБезНДС КАК СтоимостьБезНДС,
	               |	ТаблицаТовары.Количество * Аналитика.УчетнаяЦена КАК СтоимостьРегл,
	               |	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	               |	&Ссылка КАК ДокументДвижения,
	               |	АналитикаКомплект.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	               |	АналитикаКомплект.АналитикаВидаУчета КАК КорАналитикаВидаУчета,
	               |	ТаблицаКомплект.Количество КАК КорКоличество
	               |ИЗ
	               |	ВтТаблицаТовары КАК ТаблицаТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтАналитикаУчетныеЦены КАК Аналитика
	               |		ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	               |			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	               |			И ТаблицаТовары.ПартияДляСебестоимости = Аналитика.Партия,
	               |	ВтТаблицаКомплект КАК ТаблицаКомплект
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтАналитикаУчетныеЦены КАК АналитикаКомплект
	               |		ПО ТаблицаКомплект.Номенклатура = АналитикаКомплект.Номенклатура
	               |			И ТаблицаКомплект.СерияНоменклатурыДляСебестоимости = АналитикаКомплект.СерияНоменклатуры
	               |			И ТаблицаКомплект.ПартияДляСебестоимости = АналитикаКомплект.Партия
	               |ГДЕ
	               |	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	2,
	               |	ТаблицаКомплект.НомерСтроки,
	               |	&Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	               |	Аналитика.АналитикаУчетаНоменклатуры,
	               |	Аналитика.АналитикаВидаУчета,
	               |	ТаблицаКомплект.Количество,
	               |	ТоварыСтоимость.Стоимость,
	               //|	ТоварыСтоимость.СтоимостьБезНДС,
				   |	ТоварыСтоимость.Стоимость,
	               |	ТоварыСтоимость.Стоимость,
	               |	&ХозяйственнаяОперация,
	               |	НЕОПРЕДЕЛЕНО,
	               |	НЕОПРЕДЕЛЕНО,
	               |	НЕОПРЕДЕЛЕНО,
	               |	0
	               |ИЗ
	               |	ВтТаблицаКомплект КАК ТаблицаКомплект
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтАналитикаУчетныеЦены КАК Аналитика
	               |		ПО ТаблицаКомплект.Номенклатура = Аналитика.Номенклатура
	               |			И ТаблицаКомплект.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	               |			И ТаблицаКомплект.ПартияДляСебестоимости = Аналитика.Партия,
	               |	(ВЫБРАТЬ
	               |		СУММА(ТаблицаТовары.Количество * Аналитика.УчетнаяЦена) КАК Стоимость,
				   |		СУММА(ТаблицаТовары.Количество * Аналитика.УчетнаяЦенаБезНДС) КАК СтоимостьБезНДС
	               |	ИЗ
	               |		ВтТаблицаТовары КАК ТаблицаТовары
	               |			ЛЕВОЕ СОЕДИНЕНИЕ ВтАналитикаУчетныеЦены КАК Аналитика
	               |			ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	               |				И ТаблицаТовары.СерияНоменклатуры = Аналитика.СерияНоменклатуры
	               |				И ТаблицаТовары.Партия = Аналитика.Партия) КАК ТоварыСтоимость
	               |ГДЕ
	               |	ТаблицаКомплект.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	3,
	               |	ТаблицаКомплект.НомерСтроки,
	               |	&Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	               |	Аналитика.АналитикаУчетаНоменклатуры,
	               |	Аналитика.АналитикаВидаУчета,
	               |	ТаблицаКомплект.Количество,
	               |	ТаблицаКомплект.Количество * Аналитика.УчетнаяЦена,
	               |	ТаблицаКомплект.Количество * Аналитика.УчетнаяЦенаБезНДС,
	               |	ТаблицаКомплект.Количество * Аналитика.УчетнаяЦена,
	               |	&ХозяйственнаяОперация,
	               |	НЕОПРЕДЕЛЕНО,
	               |	НЕОПРЕДЕЛЕНО,
	               |	НЕОПРЕДЕЛЕНО,
	               |	0
	               |ИЗ
	               |	ВтТаблицаКомплект КАК ТаблицаКомплект
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтАналитикаУчетныеЦены КАК Аналитика
	               |		ПО ТаблицаКомплект.Номенклатура = Аналитика.Номенклатура
	               |			И ТаблицаКомплект.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	               |			И ТаблицаКомплект.ПартияДляСебестоимости = Аналитика.Партия
	               |ГДЕ
	               |	ТаблицаКомплект.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	4,
	               |	ТаблицаТовары.НомерСтроки,
	               |	&Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	               |	Аналитика.АналитикаУчетаНоменклатуры,
	               |	Аналитика.АналитикаВидаУчета,
	               |	ТаблицаТовары.Количество,
	               |	ТаблицаТовары.Количество * ТаблицаТовары.Цена,
	               |	ТаблицаТовары.Количество * ТаблицаТовары.Цена,
	               |	ТаблицаТовары.Количество * ТаблицаТовары.Цена,
	               |	&ХозяйственнаяОперация,
	               |	&Ссылка,
	               |	АналитикаКомплект.АналитикаУчетаНоменклатуры,
	               |	АналитикаКомплект.АналитикаВидаУчета,
	               |	ТаблицаКомплект.Количество
	               |ИЗ
	               |	ВтТаблицаТовары КАК ТаблицаТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтАналитикаУчетныеЦены КАК Аналитика
	               |		ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	               |			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	               |			И ТаблицаТовары.ПартияДляСебестоимости = Аналитика.Партия,
	               |	ВтТаблицаКомплект КАК ТаблицаКомплект
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВтАналитикаУчетныеЦены КАК АналитикаКомплект
	               |		ПО ТаблицаКомплект.Номенклатура = АналитикаКомплект.Номенклатура
	               |			И ТаблицаКомплект.СерияНоменклатурыДляСебестоимости = АналитикаКомплект.СерияНоменклатуры
	               |			И ТаблицаКомплект.ПартияДляСебестоимости = АналитикаКомплект.Партия
	               |ГДЕ
	               |	ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Порядок,
	               |	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия
	|ИЗ
	|	Документ.КомплектацияНоменклатуры.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	Партия
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("СтатусУчетСебестоимостиПоСериям", Реквизиты.СтатусУчетСебестоимостиПоСериям);
	Запрос.УстановитьПараметр("СтатусУчетСебестоимостиПоПартиям", Реквизиты.СтатусУчетСебестоимостиПоПартиям);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиУчетаНоменклатуры.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиВидаУчета(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	&Склад КАК Склад
	|ПОМЕСТИТЬ втТаблицаАналитики
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	&Склад
	|ИЗ
	|	Документ.КомплектацияНоменклатуры.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Склад
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Склад", Реквизиты.Склад);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиВидаУчета.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ИнициализироватьПартииНоменклатурыВДокументе(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаТовары.Ссылка КАК ДокументОприходования,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Поставщик
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетПоПартиям, &СтатусУчетСебестоимостиПоПартиям)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОприходования,
	|	Поставщик
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("СтатусУчетПоПартиям", Реквизиты.СтатусУчетПоПартиям);
	Запрос.УстановитьПараметр("СтатусУчетСебестоимостиПоПартиям", Реквизиты.СтатусУчетСебестоимостиПоПартиям);
	Запрос.Выполнить();
	
	Справочники.ПартииНоменклатуры.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти // Проведение

////////////////////////////////////////////////////////////////////////////////
// Печать
#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	УправлениеПечатьюПоддержкаПроектов.ДобавитьКомандыПечати(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыПечати);
	
КонецПроцедуры

// Возвращает список доступных печатных форм документа
//
Функция ДоступныеПечатныеФормы() Экспорт
	
	ПечатныеФормы = УправлениеПечатьюПоддержкаПроектов.СоздатьКоллекциюДоступныхПечатныхФорм();
	
	Обработки.ПечатьПриходныйОрдер0504207.ДобавитьПечатнуюФорму(ПечатныеФормы);
	Обработки.ПечатьАктСписания0504230.ДобавитьПечатнуюФорму(ПечатныеФормы);
	
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	МенеджерПечати      = МетаданныеДокумента.ПолноеИмя();
	МетаданныеМакетов   = МетаданныеДокумента.Макеты;
	
	ПечатнаяФорма = УправлениеПечатьюПоддержкаПроектов.ДобавитьПечатнуюФорму(ПечатныеФормы, "АктВыпускаПродукции", МенеджерПечати);
	ПечатнаяФорма.Представление = МетаданныеМакетов.ПФ_MXL_АктВыпускаПродукции.Представление();
	ПечатнаяФорма.ПутьКМакету = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(МетаданныеМакетов.ПФ_MXL_АктВыпускаПродукции);
	УправлениеПечатьюПоддержкаПроектов.ДобавитьКомандуПечати(ПечатнаяФорма);
	
	Возврат ПечатныеФормы;
	
КонецФункции

Функция ПолучитьДанныеДляПечатиАкта(МассивОбъектов, ПараметрыПечати = Неопределено) Экспорт
	
	ПолучатьЦены = Ложь;
	ПолучатьЦеныПоНастройкамСклада = Ложь;
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
		ПолучатьЦеныПоНастройкамСклада = ПараметрыПечати.Свойство("ПолучатьЦеныПоНастройкамСклада");
		ПолучатьЦены = ПолучатьЦеныПоНастройкамСклада Или ПараметрыПечати.Свойство("ПолучатьЦены");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеДляПечатиАкта(ПолучатьЦены, ПолучатьЦеныПоНастройкамСклада);
	Запрос.УстановитьПараметр("ТекущийДокумент", МассивОбъектов);;
	Запрос.УстановитьПараметр("ПолучатьЦеныПоНастройкамСклада", ПолучатьЦеныПоНастройкамСклада);
	ЗапасыСервер.УстановитьСтатусыПараметровУчетаВПараметрахЗапроса(Запрос);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке"          , РезультатыЗапросов[РезультатыЗапросов.ВГраница() - 1]);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти" , РезультатыЗапросов[РезультатыЗапросов.ВГраница()]);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

Функция ПолучитьДанныеДляПечати(МассивОбъектов, ПараметрыПечати = Неопределено) Экспорт
	
	ПолучатьЦены = Ложь;
	ПолучатьЦеныПоНастройкамСклада = Ложь;
	ДанныеСписания = Ложь;
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
		ПолучатьЦеныПоНастройкамСклада = ПараметрыПечати.Свойство("ПолучатьЦеныПоНастройкамСклада");
		ПолучатьЦены = ПолучатьЦеныПоНастройкамСклада Или ПараметрыПечати.Свойство("ПолучатьЦены");
		ДанныеСписания = ПараметрыПечати.Свойство("ДанныеСписания");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ?(ДанныеСписания, ТекстЗапросаДанныеДляПечатиСписание(ПолучатьЦены, ПолучатьЦеныПоНастройкамСклада), ТекстЗапросаДанныеДляПечати(ПолучатьЦены, ПолучатьЦеныПоНастройкамСклада));
	Запрос.УстановитьПараметр("ТекущийДокумент", МассивОбъектов);;
	Запрос.УстановитьПараметр("ПолучатьЦеныПоНастройкамСклада", ПолучатьЦеныПоНастройкамСклада);
	ЗапасыСервер.УстановитьСтатусыПараметровУчетаВПараметрахЗапроса(Запрос);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке"          , РезультатыЗапросов[РезультатыЗапросов.ВГраница() - 1]);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти" , РезультатыЗапросов[РезультатыЗапросов.ВГраница()]);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

Функция ТекстЗапросаДанныеДляПечатиАкта(ПолучатьЦены = Ложь, ПолучатьЦеныПоНастройкамСклада = Ложь)
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Документ.Ссылка КАК Ссылка,
	               |	Документ.Номер КАК НомерДокумента,
	               |	Документ.Дата КАК ДатаДокумента,
	               |	Документ.Организация КАК Организация,
	               |	Документ.Склад КАК Склад,
	               |	Документ.Склад.Представление КАК СкладПредставление,
	               |	Документ.Номенклатура КАК Номенклатура,
	               |	Документ.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Документ.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Документ.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Документ.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Документ.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Документ.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Документ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Документ.Количество КАК Количество,
	               |	Документ.Ответственный КАК Ответственный
	               |ПОМЕСТИТЬ втШапка
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры КАК Документ
	               |ГДЕ
	               |	Документ.Ссылка В(&ТекущийДокумент)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Товары.Ссылка КАК Документ,
	               |	Товары.НомерСтроки КАК НомерСтроки,
	               |	КОНЕЦПЕРИОДА(Шапка.ДатаДокумента, ДЕНЬ) КАК ДатаПолученияЦены,
	               |	Шапка.Организация КАК Организация,
	               |	Шапка.Склад КАК Склад,
	               |	Товары.Номенклатура КАК Номенклатура,
	               |	Товары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Товары.Номенклатура.Код КАК ТоварКод,
	               |	Товары.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Товары.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Товары.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Товары.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Товары.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Товары.ЕдиницаИзмерения.КодОКЕИ КАК КодПоОКЕИ,
	               |	Товары.Количество КАК Количество
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры.Товары КАК Товары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втШапка КАК Шапка
	               |		ПО Товары.Ссылка = Шапка.Ссылка
	               |ГДЕ
	               |	Товары.Ссылка В(&ТекущийДокумент)";
	
	ПараметрыПолученияЦен = ФормированиеПечатныхФормПоддержкаПроектов.ПараметрыПолученияЦен();
	
	Если ПолучатьЦены Тогда
		
		ПараметрыПолученияЦен.ИспользоватьЦеныПоВидуЦен = Истина;
		Если ПолучатьЦеныПоНастройкамСклада Тогда
			ПараметрыПолученияЦен.ИспользоватьЦеныПоСебестоимости = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса = ФормированиеПечатныхФормПоддержкаПроектов.ТекстЗапросаСЦенамиБезНаправления(ТекстЗапроса, ПараметрыПолученияЦен);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеДляПечати(ПолучатьЦены = Ложь, ПолучатьЦеныПоНастройкамСклада = Ложь)
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Документ.Ссылка КАК Ссылка,
	               |	Документ.Номер КАК НомерДокумента,
	               |	Документ.Дата КАК ДатаДокумента,
	               |	Документ.Организация КАК Организация,
				   |	Документ.Организация КАК Отправитель,
				   |	Документ.Склад КАК Получатель,
	               |	Документ.Склад КАК Склад,
				   |	Документ.Склад КАК СкладОтправитель,
				   |	Документ.Склад КАК СкладПолучатель,
	               |	Документ.Номенклатура КАК Номенклатура,
	               |	Документ.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Документ.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Документ.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Документ.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Документ.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Документ.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Документ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Документ.Количество КАК Количество,
	               |	Документ.Ответственный КАК Исполнитель,
				   |	Документ.Ответственный.ФизическоеЛицо.Уточнение КАК ИсполнительДолжность,
				   |	"""" КАК НомерВходящегоДокумента,
				   |	ДАТАВРЕМЯ(1,1,1) КАК ДатаВходящегоДокумента
	               |ПОМЕСТИТЬ втШапка
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры КАК Документ
	               |ГДЕ
	               |	Документ.Ссылка В(&ТекущийДокумент)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Товары.Ссылка КАК Документ,
	               |	Товары.НомерСтроки КАК НомерСтроки,
	               |	КОНЕЦПЕРИОДА(Шапка.ДатаДокумента, ДЕНЬ) КАК ДатаПолученияЦены,
	               |	Шапка.Организация КАК Организация,
	               |	Шапка.Склад КАК Склад,
	               |	Товары.Номенклатура КАК Номенклатура,
	               |	Товары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Товары.Номенклатура.Код КАК ТоварКод,
	               |	Товары.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Товары.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Товары.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Товары.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Товары.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Товары.ЕдиницаИзмерения.КодОКЕИ КАК КодПоОКЕИ,
	               |	Товары.Количество КАК Количество,
				   |	Товары.Номенклатура.Описание КАК Описание
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры.Товары КАК Товары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втШапка КАК Шапка
	               |		ПО Товары.Ссылка = Шапка.Ссылка
	               |ГДЕ
	               |	Товары.Ссылка В(&ТекущийДокумент)
				   |	И Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
				   |
				   |ОБЪЕДИНИТЬ ВСЕ
				   |
				   |ВЫБРАТЬ
	               |	Документ.Ссылка КАК Документ,
	               |	1 КАК НомерСтроки,
	               |	КОНЕЦПЕРИОДА(Документ.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	               |	Документ.Организация КАК Организация,
	               |	Документ.Склад КАК Склад,
	               |	Документ.Номенклатура КАК Номенклатура,
	               |	Документ.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Документ.Номенклатура.Код КАК ТоварКод,
	               |	Документ.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Документ.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Документ.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Документ.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Документ.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Документ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Документ.ЕдиницаИзмерения.КодОКЕИ КАК КодПоОКЕИ,
	               |	Документ.Количество КАК Количество,
				   |	Документ.Номенклатура.Описание КАК Описание
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры КАК Документ
	               |ГДЕ
	               |	Товары.Ссылка В(&ТекущийДокумент)
				   |	И Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
				   |";
	
	ПараметрыПолученияЦен = ФормированиеПечатныхФормПоддержкаПроектов.ПараметрыПолученияЦен();
	
	Если ПолучатьЦены Тогда
		
		ПараметрыПолученияЦен.ИспользоватьЦеныПоВидуЦен = Истина;
		Если ПолучатьЦеныПоНастройкамСклада Тогда
			ПараметрыПолученияЦен.ИспользоватьЦеныПоСебестоимости = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса = ФормированиеПечатныхФормПоддержкаПроектов.ТекстЗапросаСЦенамиБезНаправления(ТекстЗапроса, ПараметрыПолученияЦен);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеДляПечатиСписание(ПолучатьЦены = Ложь, ПолучатьЦеныПоНастройкамСклада = Ложь)
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Документ.Ссылка КАК Ссылка,
	               |	Документ.Номер КАК НомерДокумента,
	               |	Документ.Дата КАК ДатаДокумента,
	               |	Документ.Организация КАК Организация,
				   |	Документ.Организация КАК Отправитель,
				   |	Документ.Организация КАК Получатель,
	               |	Документ.Склад КАК Склад,
				   |	Документ.Склад КАК СкладОтправитель,
				   |	Документ.Склад КАК СкладПолучатель,
	               |	Документ.Номенклатура КАК Номенклатура,
	               |	Документ.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Документ.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Документ.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Документ.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Документ.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Документ.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Документ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Документ.Количество КАК Количество,
	               |	Документ.Ответственный КАК Исполнитель,
				   |	Документ.Ответственный.ФизическоеЛицо.Уточнение КАК ИсполнительДолжность,
				   |	"""" КАК НомерВходящегоДокумента,
				   |	ДАТАВРЕМЯ(1,1,1) КАК ДатаВходящегоДокумента
	               |ПОМЕСТИТЬ втШапка
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры КАК Документ
	               |ГДЕ
	               |	Документ.Ссылка В(&ТекущийДокумент)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Товары.Ссылка КАК Документ,
	               |	Товары.НомерСтроки КАК НомерСтроки,
	               |	КОНЕЦПЕРИОДА(Шапка.ДатаДокумента, ДЕНЬ) КАК ДатаПолученияЦены,
	               |	Шапка.Организация КАК Организация,
	               |	Шапка.Склад КАК Склад,
	               |	Товары.Номенклатура КАК Номенклатура,
	               |	Товары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Товары.Номенклатура.Код КАК ТоварКод,
	               |	Товары.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Товары.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Товары.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Товары.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Товары.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Товары.ЕдиницаИзмерения.КодОКЕИ КАК КодПоОКЕИ,
	               |	Товары.Количество КАК Количество,
				   |	Товары.Номенклатура.Описание КАК Описание,
				   |	""Укомплектовано"" КАК ПричинаСписания
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры.Товары КАК Товары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втШапка КАК Шапка
	               |		ПО Товары.Ссылка = Шапка.Ссылка
	               |ГДЕ
	               |	Товары.Ссылка В(&ТекущийДокумент)
				   |	И Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
				   |
				   |ОБЪЕДИНИТЬ ВСЕ
				   |
				   |ВЫБРАТЬ
	               |	Документ.Ссылка КАК Документ,
	               |	1 КАК НомерСтроки,
	               |	КОНЕЦПЕРИОДА(Документ.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	               |	Документ.Организация КАК Организация,
	               |	Документ.Склад КАК Склад,
	               |	Документ.Номенклатура КАК Номенклатура,
	               |	Документ.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Документ.Номенклатура.Код КАК ТоварКод,
	               |	Документ.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Документ.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	ЕСТЬNULL(Документ.СерияНоменклатуры.Наименование, """") КАК НомерСерии,
	               |	Документ.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Документ.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Документ.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Документ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Документ.ЕдиницаИзмерения.КодОКЕИ КАК КодПоОКЕИ,
	               |	Документ.Количество КАК Количество,
				   |	Документ.Номенклатура.Описание КАК Описание,
				   |	""Разукомплектовано"" КАК ПричинаСписания
	               |ИЗ
	               |	Документ.КомплектацияНоменклатуры КАК Документ
	               |ГДЕ
	               |	Товары.Ссылка В(&ТекущийДокумент)
				   |	И Товары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
				   |";
	
	ПараметрыПолученияЦен = ФормированиеПечатныхФормПоддержкаПроектов.ПараметрыПолученияЦен();
	
	Если ПолучатьЦены Тогда
		
		ПараметрыПолученияЦен.ИспользоватьЦеныПоВидуЦен = Истина;
		Если ПолучатьЦеныПоНастройкамСклада Тогда
			ПараметрыПолученияЦен.ИспользоватьЦеныПоСебестоимости = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса = ФормированиеПечатныхФормПоддержкаПроектов.ТекстЗапросаСЦенамиБезНаправления(ТекстЗапроса, ПараметрыПолученияЦен);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПечатьАктВыпускаПродукции(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	
	ПолноеИмяМакета = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(ПустаяСсылка().Метаданные().Макеты.ПФ_MXL_АктВыпускаПродукции);
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_" + ПолноеИмяМакета;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПолноеИмяМакета);
	
	МассивВыводимыхОбластей = Новый Массив;
	
	ВалютаПечати = ЗначениеНастроекПоддержкаПроектовПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ПолучатьЦеныПоНастройкамСклада");
	ДанныеДляПечати = ПолучитьДанныеДляПечатиАкта(МассивОбъектов, ПараметрыПечати);
	
	Шапка = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(Шапка.Ссылка, ТабличныйДокумент, Макет);
		
		// Получение параметров для заполнения
		ПараметрыИзШапки = ПолучитьПараметрыШапкиАктВыпускаПродукции(Шапка);
		
		// Вывод области Заголовок
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Заголовок", ПараметрыИзШапки);
		
		// Вывод области РеквизитыШапки
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "РеквизитыШапки", ПараметрыИзШапки);
		
		// Вывод области ШапкаТаблицы
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "ШапкаТаблицы", ПараметрыИзШапки);
		
		// Инициализация итогов по документу
		ПараметрыИтого = Новый Структура;
		ПараметрыИтого.Вставить("Сумма", 0);
		
		ВыборкаПоДокументам.Сбросить();
		ПараметрыПоиска = Новый Структура("Документ", Шапка.Ссылка);
		Если ВыборкаПоДокументам.НайтиСледующий(ПараметрыПоиска) Тогда
			ВыборкаСтрокТовары = ВыборкаПоДокументам.Выбрать();
			КлючиПараметров = ФормированиеПечатныхФормПоддержкаПроектов.ПолучитьИменаКолонокТаблицы(ВыборкаСтрокТовары);
			
			// Формирование области Строка
			МассивСтрокТаблицы = Новый Массив;
			КоличествоСтрок = ВыборкаСтрокТовары.Количество();
			НомерСтроки = 0;
			Пока ВыборкаСтрокТовары.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				
				ДанныеСтроки = Новый Структура(КлючиПараметров);
				ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВыборкаСтрокТовары);
				
				ТоварНаименование = ОбщегоНазначенияПоддержкаПроектов.ПолучитьПредставлениеНоменклатурыДляПечати(
					ВыборкаСтрокТовары.ТоварНаименование,
					ВыборкаСтрокТовары.СерияНоменклатуры,
					ВыборкаСтрокТовары.Партия);
				ДанныеСтроки.Вставить("ТоварНаименование", ТоварНаименование);
				
				ФормированиеПечатныхФормПоддержкаПроектов.ДобавитьОбластьВМассивПоОписанию(Макет, "Строка", МассивСтрокТаблицы, ДанныеСтроки);
				
				ФормированиеПечатныхФормПоддержкаПроектов.РассчитатьИтоги(ДанныеСтроки, ПараметрыИтого);
				
			КонецЦикла;
		КонецЕсли;
		
		// Вывод области СтрокаПродукция
		ПараметрыИзШапки.Вставить("Цена" , ПараметрыИтого.Сумма / ?(Шапка.Количество = 0, 1, Шапка.Количество));
		ПараметрыИзШапки.Вставить("Сумма", ПараметрыИтого.Сумма);
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "СтрокаПродукция", ПараметрыИзШапки);
		
		// Вывод области Строка
		ГраницаОбластейСтрока = МассивСтрокТаблицы.ВГраница();
		Для ИндексОбласти = 0 По ГраницаОбластейСтрока Цикл
			
			ОбластьСтрока = МассивСтрокТаблицы[ИндексОбласти];
			
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(ОбластьСтрока);
			Если ИндексОбласти = ГраницаОбластейСтрока Тогда
				МассивВыводимыхОбластей.Добавить(Макет.ПолучитьОбласть("Итого"));
				МассивВыводимыхОбластей.Добавить(Макет.ПолучитьОбласть("СуммаПрописью"));
				МассивВыводимыхОбластей.Добавить(Макет.ПолучитьОбласть("Подписи"));
			КонецЕсли;
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, МассивВыводимыхОбластей) Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "ШапкаТаблицы", ПараметрыИзШапки);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		// Вывод области Итого
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Итого", ПараметрыИтого);
		
		// Вывод области СуммаПрописью
		ФорматированнаяСумма = ОбщегоНазначенияПоддержкаПроектов.ФорматСумм(ПараметрыИтого.Сумма, ВалютаПечати);
		Использованополучено = ?(Шапка.Ссылка.ВидОперации = Перечисления.ВидыОперацийКомплектацияНоменклатуры.Комплектация, "использовано", "получено");
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего %3 наименований %1, на сумму %2'"), КоличествоСтрок, ФорматированнаяСумма, ИспользованоПолучено);
		
		ПараметрыСуммаПрописью = Новый Структура;
		ПараметрыСуммаПрописью.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		ПараметрыСуммаПрописью.Вставить("СуммаПрописью" , РаботаСКурсамиВалют.СформироватьСуммуПрописью(ПараметрыИтого.Сумма, ВалютаПечати));
		
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "СуммаПрописью", ПараметрыСуммаПрописью);
		
		// Вывод области Подписи
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Подписи", ПараметрыИзШапки);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьПараметрыШапкиАктВыпускаПродукции(Шапка)
	
	КлючиПараметров = ФормированиеПечатныхФормПоддержкаПроектов.ПолучитьИменаКолонокТаблицы(Шапка);
	
	Параметры = Новый Структура(КлючиПараметров);
	ЗаполнитьЗначенияСвойств(Параметры, Шапка);
	
	Параметры.Ответственный = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(Шапка.Ответственный.Наименование);
	
	ТоварНаименование = ОбщегоНазначенияПоддержкаПроектов.ПолучитьПредставлениеНоменклатурыДляПечати(
		Шапка.ТоварНаименование,
		Шапка.СерияНоменклатуры,
		Шапка.Партия);
	Параметры.Вставить("ТоварНаименование", ТоварНаименование);
	
	// Данные заголовка
	НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента);
	ШаблонТекстЗаголовка = НСтр("ru = 'Акт " + ?(Шапка.Ссылка.ВидОперации = Перечисления.ВидыОперацийКомплектацияНоменклатуры.Комплектация, "комплектации", "разукомплектации") + " номенклатуры № %1 от %2'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстЗаголовка, НомерДокумента, Формат(Шапка.ДатаДокумента, "ДЛФ=DD"));
	
	Параметры.Вставить("ТекстЗаголовка"      , ТекстЗаголовка);
	Параметры.Вставить("НомерСтрокиПродукции", 1);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти // Печать

////////////////////////////////////////////////////////////////////////////////
// Команды формы
#Область КомандыФормы

// Заполняет список команд ввода на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыСоздатьНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы) Экспорт
	
	ВводНаОснованииПоддержкаПроектов.ДобавитьКомандыСоздатьНаОсновании(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыСоздатьНаОсновании, НастройкиФормы);
	
КонецПроцедуры

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, НастройкиФормы) Экспорт
	
	МенюОтчетыПоддержкаПроектов.ДобавитьОбщиеКоманды(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыОтчетов, НастройкиФормы);
	
КонецПроцедуры

#КонецОбласти // КомандыФормы

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти // СтандартныеПодсистемы

////////////////////////////////////////////////////////////////////////////////
// ОБНОВЛЕНИЕ ИНФОРМАЦИОННОЙ БАЗЫ
#Область ОбновлениеИнформационнойБазы

// Обработчик обновления релиза 2.0.1.17
// Регистрирует документы перемещения товаров для заполнения хозяйственной операции.
//
Процедура ЗаполнитьХозяйственнуюОперациюОтложенноДанныеДляОбновления(Параметры) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК Документ
	|ГДЕ
	|	Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

// Обработчик обновления релиза 2.0.1.17
// Заполняет хозяйственную операцию в документах перемещения товаров.
//
Процедура ЗаполнитьХозяйственнуюОперациюОтложенно(Параметры) Экспорт
	
	МетаданныеОбъекта = ПустаяСсылка().Метаданные();
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Документ.Ссылка                 КАК Ссылка,
	|	Документ.ХозяйственнаяОперация  КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			#ДокументыКОбработке КАК ДокументыКОбработке
	|		ПО
	|			Документ.Ссылка = ДокументыКОбработке.Ссылка
	|";
	
	Запрос.Текст = СтрЗаменить(ЗАпрос.Текст, "#ДокументыКОбработке", Результат.ИмяВременнойТаблицы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ХозяйственнаяОперация) Тогда
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				
				ОтменитьТранзакцию();
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				Продолжить;
				
			КонецЕсли;
			
			ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать документ: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%",  Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти // ОбновлениеИнформационнойБазы

#КонецЕсли