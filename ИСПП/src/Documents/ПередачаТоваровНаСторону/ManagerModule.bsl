#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Имена реквизитов, от значений которых зависят параметры учета номенклатуры
//
// Возвращаемое значение:
//   Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУчетаНоменклатуры() Экспорт
	
	Возврат "Склад";
	
КонецФункции

// Возвращает параметры учета для номенклатуры, указанной в документе
//
// Параметры
//   Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
// Возвращаемое значение
//   Структура - Состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУчетаНоменклатуры
//
Функция ПараметрыУчетаНоменклатуры(Объект) Экспорт
	
	ПараметрыУчета = ЗапасыСервер.ПараметрыУчетаНоменклатуры();
	ПараметрыУчета.ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ПараметрыУчетаНаСкладе = СкладыСервер.ПараметрыУчетаНоменклатуры(Объект.Склад);
	ПараметрыУчета.ИспользоватьСерии = ПараметрыУчетаНаСкладе.ИспользоватьСерииНоменклатуры;
	ПараметрыУчета.ИспользоватьПартии = ПараметрыУчетаНаСкладе.ИспользоватьПартии;
	ПараметрыУчета.Склад = Объект.Склад;
	
	Возврат ПараметрыУчета;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания параметров учета номенклатуры
//
// Параметры
//   ПараметрыУчетаНоменклатуры - Структура - состав полей задается в функции ЗапасыСервер.ПараметрыУчетаНоменклатуры
//
// Возвращаемое значение
//   Строка - текст запроса
//
Функция ТекстЗапросаРасчетаСтатусовУчетаНоменклатуры(ПараметрыУчетаНоменклатуры) Экспорт
	
	Возврат ЗапасыСервер.ТекстЗапросаРасчетаСтатусовУчетаНоменклатуры(ПараметрыУчетаНоменклатуры);
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение
#Область Проведение

// Инициализирует таблицы значений, содержащие данные для проведения документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицыДвиженийДляПроведения(ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ОсновныеДанныеДокумента = ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства);
	
	ИнициализироватьКлючиАналитикиВидаУчета(ОсновныеДанныеДокумента);
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(ОсновныеДанныеДокумента);
	
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтТаблицаТовары());
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаТоварыНаСкладах(), Метаданные.РегистрыНакопления.ТоварыНаСкладах);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаСвободныеОстатки(), Метаданные.РегистрыНакопления.СвободныеОстатки);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтАналитика());
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВтАналитикаУчетныеЦены());
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаСебестоимостьТоваров(), Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаДвиженияНоменклатураДоходыРасходы(), Метаданные.РегистрыНакопления.ДвиженияНоменклатураДоходыРасходы);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаВыручкаИСебестоимостьПродаж(), Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаТаблицаСписано(), Метаданные.РегистрыНакопления.ИсполнениеЗаявокПредприятий);
	ПроведениеПоддержкаПроектов.ДобавитьТекстЗапросаДвижений(ДополнительныеСвойства, ТекстЗапросаТаблицаДетализацииСписанияПоТипам(), Метаданные.РегистрыНакопления.ДетализацияСписанияМСИПоТипам);
	
	Запрос = Новый Запрос(ПроведениеПоддержкаПроектов.ПолучитьТекстЗапросаДвижений(ДополнительныеСвойства, Регистры));
	
	Для Каждого ДанныеДокумента Из ОсновныеДанныеДокумента Цикл
		Запрос.УстановитьПараметр(ДанныеДокумента.Ключ, ДанныеДокумента.Значение);
	КонецЦикла;
	
	ПроведениеПоддержкаПроектов.ЗаполнитьТаблицыДвижений(ДополнительныеСвойства, Запрос.ВыполнитьПакет(), Регистры);
	
КонецПроцедуры

Функция ПодготовитьОсновныеДанныеДляПроведения(ДополнительныеСвойства)
	
	ЗапрашиваемыеДанные = Новый Структура;
	ЗапрашиваемыеДанные.Вставить("Ссылка");
	ЗапрашиваемыеДанные.Вставить("Период", "Дата");
	ЗапрашиваемыеДанные.Вставить("Организация");
	ЗапрашиваемыеДанные.Вставить("Склад");
	ЗапрашиваемыеДанные.Вставить("ПодразделениеОрганизации");
	ЗапрашиваемыеДанные.Вставить("ХозяйственнаяОперация");
	ЗапрашиваемыеДанные.Вставить("СтатьяРасходов");
	ЗапрашиваемыеДанные.Вставить("АналитикаРасходов");
	ЗапрашиваемыеДанные.Вставить("Контрагент");
	ЗапрашиваемыеДанные.Вставить("ДоговорКонтрагента");
	
	ОсновныеДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПроведениеПоддержкаПроектов.ПолучитьСсылкуНаДокументДляПроведения(ДополнительныеСвойства),
		ЗапрашиваемыеДанные);
	
	ОсновныеДанныеДокумента.Вставить("ЭтоБезвозмезднаяПередача", ЭтоБезвозмезднаяПередача(ОсновныеДанныеДокумента.ХозяйственнаяОперация));
	ОсновныеДанныеДокумента.Вставить("Граница", Новый Граница(ОсновныеДанныеДокумента.Ссылка.МоментВремени(), ВидГраницы.Исключая));
	
	ЗапасыСервер.ПриПодготовкеОсновныхДанныхДляПроведения(ДополнительныеСвойства, ОсновныеДанныеДокумента);
	
	Возврат ОсновныеДанныеДокумента;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаТовары()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                               КАК НомерСтроки,
	|	&Организация                                            КАК Организация,
	|	&Склад                                                  КАК Склад,
	|	ТаблицаТовары.Номенклатура                              КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетПоСериям, &СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                   КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетПоПартиям, &СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                   КАК Партия,
	|	ТаблицаТовары.Количество                                КАК Количество,
	|	ТаблицаТовары.СуммаСНДС                                 КАК СуммаСНДС,
	|	ТаблицаТовары.СуммаСНДС - ТаблицаТовары.СуммаНДС        КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                   КАК СерияНоменклатурыДляСебестоимости,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                   КАК ПартияДляСебестоимости
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.ПередачаТоваровНаСторону.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаТоварыНаСкладах()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаТовары.Организация               КАК Организация,
	|	ТаблицаТовары.Склад                     КАК Склад,
	|	ТаблицаТовары.Номенклатура              КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры         КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                    КАК Партия,
	|	ТаблицаТовары.Количество                КАК Количество
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаТаблицаСписано()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПередачаТоваровНаСторонуТовары.Партия.ДокументОприходования.ЗаказПоставщику КАК СпецификацияКДоговору,
	               |	ПередачаТоваровНаСторонуТовары.Ссылка.Склад КАК Предприятие,
	               |	ПередачаТоваровНаСторонуТовары.Номенклатура КАК Номенклатура,
	               |	ПередачаТоваровНаСторонуТовары.Количество КАК Количество,
	               |	ЗНАЧЕНИЕ(Перечисление.ТипОперацииИсполненияЗаявкиПредприятия.СписаниеНаСторону) КАК ТипОперации,
	               |	&Период КАК Период,
	               |	ПередачаТоваровНаСторонуТовары.СерияНоменклатуры КАК СерияНоменклатуры
	               |ИЗ
	               |	Документ.ПередачаТоваровНаСторону.Товары КАК ПередачаТоваровНаСторонуТовары
	               |ГДЕ
	               |	ПередачаТоваровНаСторонуТовары.Ссылка = &Ссылка
	               |	И ПередачаТоваровНаСторонуТовары.Партия.ДокументОприходования ССЫЛКА Документ.ПоступлениеТоваров
	               |	И ПередачаТоваровНаСторонуТовары.Партия.ДокументОприходования.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.СпецификацияКДоговору.ПустаяСсылка)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСвободныеОстатки()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки               КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаТовары.Организация               КАК Организация,
	|	ТаблицаТовары.Склад                     КАК Склад,
	|	ТаблицаТовары.Номенклатура              КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатуры         КАК СерияНоменклатуры,
	|	ТаблицаТовары.Партия                    КАК Партия,
	|	ТаблицаТовары.Количество                КАК ВНаличии
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВтАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура                                               КАК Номенклатура,
	|	ТаблицаТовары.СерияНоменклатурыДляСебестоимости                          КАК СерияНоменклатуры,
	|	ТаблицаТовары.ПартияДляСебестоимости                                     КАК Партия,
	|	АналитикаУчетаНоменклатуры.КлючАналитики                                 КАК АналитикаУчетаНоменклатуры,
	|	АналитикаВидаУчета.КлючАналитики                                         КАК АналитикаВидаУчета
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ПО
	|			ТаблицаТовары.Номенклатура                        = АналитикаУчетаНоменклатуры.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = АналитикаУчетаНоменклатуры.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости            = АналитикаУчетаНоменклатуры.Партия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаВидаУчета КАК АналитикаВидаУчета
	|		ПО
	|			АналитикаВидаУчета.Организация                = ТаблицаТовары.Организация
	|			И АналитикаВидаУчета.Склад                    = ТаблицаТовары.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	АналитикаВидаУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВтАналитикаУчетныеЦены()
	
	ТекстЗапросаУчетныеЦены = 
	"ВЫБРАТЬ
	|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры			 						КАК АналитикаУчетаНоменклатуры,
	|	СебестоимостьТоваров.АналитикаВидаУчета					 						КАК АналитикаВидаУчета,
	|	ВЫБОР КОГДА СебестоимостьТоваров.КоличествоОстаток = 0
	|		ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СебестоимостьТоваров.СтоимостьОстаток КАК ЧИСЛО(20, 9)) / ВЫРАЗИТЬ(СебестоимостьТоваров.КоличествоОстаток КАК ЧИСЛО(20, 9))
	|	КОНЕЦ 																			КАК УчетнаяЦена,
	|	ВЫБОР КОГДА СебестоимостьТоваров.КоличествоОстаток = 0                                         
	|		ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СебестоимостьТоваров.СтоимостьБезНДСОстаток КАК ЧИСЛО(20, 9)) / ВЫРАЗИТЬ(СебестоимостьТоваров.КоличествоОстаток КАК ЧИСЛО(20, 9))
	|	КОНЕЦ 																			КАК УчетнаяЦенаБезНДС
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(
	|			&Граница,
	|			(АналитикаВидаУчета, АналитикаУчетаНоменклатуры) В
	|				(ВЫБРАТЬ
	|					Врем.АналитикаВидаУчета,
	|					Врем.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ВтАналитика КАК Врем)) КАК СебестоимостьТоваров";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Аналитика.Номенклатура								КАК Номенклатура,
	|	Аналитика.СерияНоменклатуры							КАК СерияНоменклатуры,
	|	Аналитика.Партия									КАК Партия,
	|	Аналитика.АналитикаУчетаНоменклатуры		 		КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.АналитикаВидаУчета		 				КАК АналитикаВидаУчета,
	|	ЕстьNULL(УчетныеЦеныНоменклатуры.УчетнаяЦена, 0)	КАК УчетнаяЦена,
	|	ЕстьNULL(УчетныеЦеныНоменклатуры.УчетнаяЦенаБезНДС, 0)	КАК УчетнаяЦенаБезНДС
	|ПОМЕСТИТЬ ВтАналитикаУчетныеЦены
	|ИЗ
	|	ВтАналитика КАК Аналитика
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			(" + ТекстЗапросаУчетныеЦены + ") КАК УчетныеЦеныНоменклатуры
	|		ПО 
	|			Аналитика.АналитикаУчетаНоменклатуры = УчетныеЦеныНоменклатуры.АналитикаУчетаНоменклатуры
	|			И Аналитика.АналитикаВидаУчета		 = УчетныеЦеныНоменклатуры.АналитикаВидаУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|   СерияНоменклатуры,
	|   Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСебестоимостьТоваров()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                         КАК НомерСтроки,
	|	&Период                                           КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)            КАК ВидДвижения,
	|	Аналитика.АналитикаУчетаНоменклатуры              КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.АналитикаВидаУчета                      КАК АналитикаВидаУчета,
	|	ТаблицаТовары.Количество                          КАК Количество,
	|	ТаблицаТовары.Количество * Аналитика.УчетнаяЦена 	КАК Стоимость,
	|	ТаблицаТовары.Количество * Аналитика.УчетнаяЦенаБезНДС 	КАК СтоимостьБезНДС,
	|	ТаблицаТовары.Количество * Аналитика.УчетнаяЦена 	КАК СтоимостьРегл,
	|	&ХозяйственнаяОперация                            КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                                   КАК СтатьяРасходов,
	|	&АналитикаРасходов                                КАК АналитикаРасходов,
	|	&Ссылка                                           КАК ДокументДвижения,
	|	&Контрагент                                       КАК Контрагент,
	|	&ДоговорКонтрагента                               КАК ДоговорКонтрагента,
	|	&ПодразделениеОрганизации                         КАК ПодразделениеОрганизации,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                      КАК КорАналитикаВидаУчета,
	|	0                                                 КАК КорКоличество
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ВтАналитикаУчетныеЦены КАК Аналитика
	|		ПО
	|			ТаблицаТовары.Номенклатура                        = Аналитика.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости            = Аналитика.Партия
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаДвиженияНоменклатураДоходыРасходы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                         КАК НомерСтроки,
	|	&Период                                           КАК Период,
	|	&ХозяйственнаяОперация                            КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                                   КАК СтатьяДоходовРасходов,
	|	&АналитикаРасходов                                КАК АналитикаРасходов,
	|	Аналитика.АналитикаВидаУчета                      КАК АналитикаВидаУчета,
	|	Аналитика.АналитикаУчетаНоменклатуры              КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.Количество                          КАК Количество,
	|	ТаблицаТовары.Количество * Аналитика.УчетнаяЦена 	КАК Стоимость,
	|	ТаблицаТовары.Количество * Аналитика.УчетнаяЦенаБезНДС 	КАК СтоимостьБезНДС,
	|	ТаблицаТовары.Количество * Аналитика.УчетнаяЦена 	КАК СтоимостьРегл,
	|	&Ссылка                                           КАК ДокументДвижения
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ВтАналитикаУчетныеЦены КАК Аналитика
	|		ПО
	|			ТаблицаТовары.Номенклатура                        = Аналитика.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости            = Аналитика.Партия
	|
	|ГДЕ
	|	&ЭтоБезвозмезднаяПередача
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВыручкаИСебестоимостьПродаж()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                         КАК НомерСтроки,
	|	&Период                                           КАК Период,
	|	&ХозяйственнаяОперация                            КАК ХозяйственнаяОперация,
	|	Аналитика.АналитикаУчетаНоменклатуры              КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.Организация                         КАК Организация,
	|	ТаблицаТовары.Склад                               КАК Склад,
	|	&ПодразделениеОрганизации                         КАК ПодразделениеОрганизации,
	|	&Контрагент                                       КАК Контрагент,
	|	&ДоговорКонтрагента                               КАК ДоговорКонтрагента,
	|	ТаблицаТовары.Количество                          КАК Количество,
	|	ТаблицаТовары.СуммаСНДС                           КАК Сумма,
	|	ТаблицаТовары.СуммаБезНДС                         КАК СуммаБезНДС,
	|	&Ссылка                                           КАК ДокументДвижения
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ВтАналитика КАК Аналитика
	|		ПО
	|			ТаблицаТовары.Номенклатура                        = Аналитика.Номенклатура
	|			И ТаблицаТовары.СерияНоменклатурыДляСебестоимости = Аналитика.СерияНоменклатуры
	|			И ТаблицаТовары.ПартияДляСебестоимости            = Аналитика.Партия
	|
	|ГДЕ
	|	НЕ &ЭтоБезвозмезднаяПередача
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаТаблицаДетализацииСписанияПоТипам()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПередачаТоваровНаСторонуТовары.Партия.ДокументОприходования.ЗаказПоставщику КАК СпецификацияКДоговору,
	|	ПередачаТоваровНаСторонуТовары.Ссылка.Склад КАК Предприятие,
	|	ПередачаТоваровНаСторонуТовары.Номенклатура КАК Номенклатура,
	|	ПередачаТоваровНаСторонуТовары.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыАктовСписаний.ПередачаНаСторону) КАК ТипСписания,
	|	&Период КАК Период,
	|	ПередачаТоваровНаСторонуТовары.Изделие КАК Изделие,
	|	ПередачаТоваровНаСторонуТовары.Образец КАК Образец,
	|	ПередачаТоваровНаСторонуТовары.Ссылка.Склад КАК Склад,
	|	ПередачаТоваровНаСторонуТовары.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ПередачаТоваровНаСторонуТовары.Партия,
	|	АналитикаУчетаНоменклатуры.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Документ.ПередачаТоваровНаСторону.Товары КАК ПередачаТоваровНаСторонуТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ПО ПередачаТоваровНаСторонуТовары.Номенклатура = АналитикаУчетаНоменклатуры.Номенклатура
	|		И ПередачаТоваровНаСторонуТовары.СерияНоменклатуры = АналитикаУчетаНоменклатуры.СерияНоменклатуры
	|		И ПередачаТоваровНаСторонуТовары.Партия = АналитикаУчетаНоменклатуры.Партия
	|ГДЕ
	|	ПередачаТоваровНаСторонуТовары.Ссылка = &Ссылка
	|	И ПередачаТоваровНаСторонуТовары.Партия.ДокументОприходования ССЫЛКА Документ.ПоступлениеТоваров
	|	И
	|		ПередачаТоваровНаСторонуТовары.Партия.ДокументОприходования.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.СпецификацияКДоговору.ПустаяСсылка)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	|			ТОГДА ТаблицаТовары.СерияНоменклатуры
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	|			ТОГДА ТаблицаТовары.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.ПередачаТоваровНаСторону.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	Партия
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("СтатусУчетСебестоимостиПоСериям", Реквизиты.СтатусУчетСебестоимостиПоСериям);
	Запрос.УстановитьПараметр("СтатусУчетСебестоимостиПоПартиям", Реквизиты.СтатусУчетСебестоимостиПоПартиям);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиУчетаНоменклатуры.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиВидаУчета(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация КАК Организация,
	|	&Склад КАК Склад
	|ПОМЕСТИТЬ втТаблицаАналитики
	|ИЗ
	|	Документ.ПередачаТоваровНаСторону.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Склад
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Склад", Реквизиты.Склад);
	Запрос.Выполнить();
	
	Справочники.КлючиАналитикиВидаУчета.ИнициализироватьКлючиАналитики(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти // Проведение

////////////////////////////////////////////////////////////////////////////////
// Печать
#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	УправлениеПечатьюПоддержкаПроектов.ДобавитьКомандыПечати(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыПечати);
	
КонецПроцедуры

// Возвращает список доступных печатных форм документа
//
Функция ДоступныеПечатныеФормы() Экспорт
	
	ПечатныеФормы = УправлениеПечатьюПоддержкаПроектов.СоздатьКоллекциюДоступныхПечатныхФорм();
	
	Обработки.ПечатьНакладнаяНаОтпускМатериалов0504205.ДобавитьПечатнуюФорму(ПечатныеФормы);
	Обработки.ПечатьМ15.ДобавитьПечатнуюФорму(ПечатныеФормы);
	
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	МенеджерПечати      = МетаданныеДокумента.ПолноеИмя();
	МетаданныеМакетов   = МетаданныеДокумента.Макеты;
	
	ПечатнаяФорма = УправлениеПечатьюПоддержкаПроектов.ДобавитьПечатнуюФорму(ПечатныеФормы, "ВедомостьНаСписание", МенеджерПечати);
	ПечатнаяФорма.Представление = МетаданныеМакетов.ПФ_MXL_Накладная.Представление();
	ПечатнаяФорма.ПутьКМакету = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(МетаданныеМакетов.ПФ_MXL_Накладная);
	УправлениеПечатьюПоддержкаПроектов.ДобавитьКомандуПечати(ПечатнаяФорма);
	
	ПечатнаяФорма = УправлениеПечатьюПоддержкаПроектов.ДобавитьПечатнуюФорму(ПечатныеФормы, "АктСписания", МенеджерПечати);
	ПечатнаяФорма.Представление = МетаданныеМакетов.ПФ_MXL_АктСписанияНаСторону.Представление();
	ПечатнаяФорма.ПутьКМакету = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(МетаданныеМакетов.ПФ_MXL_АктСписанияНаСторону);
	УправлениеПечатьюПоддержкаПроектов.ДобавитьКомандуПечати(ПечатнаяФорма);
	
	ПечатнаяФорма = УправлениеПечатьюПоддержкаПроектов.ДобавитьПечатнуюФорму(ПечатныеФормы, "ОписьКАктуСписания", МенеджерПечати);
	ПечатнаяФорма.Представление = МетаданныеМакетов.ПФ_MXL_ОписьКАктуСписанияНаСторону.Представление();
	ПечатнаяФорма.ПутьКМакету = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(МетаданныеМакетов.ПФ_MXL_ОписьКАктуСписанияНаСторону);
	УправлениеПечатьюПоддержкаПроектов.ДобавитьКомандуПечати(ПечатнаяФорма);

	
	Возврат ПечатныеФормы;
	
КонецФункции

Функция ПечатьВедомостьНаСписание(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	
	ПолноеИмяМакета = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(ПустаяСсылка().Метаданные().Макеты.ПФ_MXL_Накладная);
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_" + ПолноеИмяМакета;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПолноеИмяМакета);
	
	МассивВыводимыхОбластей = Новый Массив;
	
	ВалютаПечати = ЗначениеНастроекПоддержкаПроектовПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ДанныеДляПечати = ПолучитьДанныеДляПечати(МассивОбъектов);
	
	Шапка = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(Шапка.Ссылка, ТабличныйДокумент, Макет);
		
		// Получение параметров для заполнения
		ПараметрыИзШапки = ПолучитьПараметрыШапкиВедомостьНаСписание(Шапка);
		
		// Вывод области Заголовок
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Заголовок", ПараметрыИзШапки);
		
		// Вывод области РеквизитыШапки
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "РеквизитыШапки", ПараметрыИзШапки);
		
		// Вывод области ШапкаТаблицы
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "ШапкаТаблицы", ПараметрыИзШапки);
		
		// Инициализация итогов по документу
		ПараметрыИтого = Новый Структура;
		ПараметрыИтого.Вставить("Сумма", 0);
		
		ВыборкаПоДокументам.Сбросить();
		ПараметрыПоиска = Новый Структура("Документ", Шапка.Ссылка);
		Если ВыборкаПоДокументам.НайтиСледующий(ПараметрыПоиска) Тогда
			ВыборкаСтрокТовары = ВыборкаПоДокументам.Выбрать();
			
			КлючиПараметров = ФормированиеПечатныхФормПоддержкаПроектов.ПолучитьИменаКолонокТаблицы(ВыборкаСтрокТовары);
			
			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
			
			КоличествоСтрок = ВыборкаСтрокТовары.Количество();
			НомерСтроки = 0;
			Пока ВыборкаСтрокТовары.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				
				ДанныеСтроки = Новый Структура(КлючиПараметров);
				ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВыборкаСтрокТовары);
				
				ТоварНаименование = ОбщегоНазначенияПоддержкаПроектов.ПолучитьПредставлениеНоменклатурыДляПечати(
					ВыборкаСтрокТовары.ТоварНаименование,
					ВыборкаСтрокТовары.СерияНоменклатуры,
					,
					ВыборкаСтрокТовары.Описание);
				
				ДанныеСтроки.Вставить("ТоварНаименование" , ТоварНаименование);
				
				ОбластьСтрока.Параметры.Заполнить(ДанныеСтроки);
				
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(ОбластьСтрока);
				Если НомерСтроки = КоличествоСтрок Тогда
					МассивВыводимыхОбластей.Добавить(Макет.ПолучитьОбласть("Итого"));
					МассивВыводимыхОбластей.Добавить(Макет.ПолучитьОбласть("Подписи"));
				КонецЕсли;
				
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, МассивВыводимыхОбластей) Тогда
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабличныйДокумент.Вывести(Макет.ПолучитьОбласть("ШапкаТаблицы"));
				КонецЕсли;
				
				// Вывод области Строка
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				ФормированиеПечатныхФормПоддержкаПроектов.РассчитатьИтоги(ДанныеСтроки, ПараметрыИтого);
				
			КонецЦикла;
		КонецЕсли;
		
		// Вывод области Итого
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Итого", ПараметрыИтого);
		
		// Вывод области СуммаПрописью
		ФорматированнаяСумма = ОбщегоНазначенияПоддержкаПроектов.ФорматСумм(ПараметрыИтого.Сумма, ВалютаПечати);
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего выпущено наименований %1, на сумму %2'"), КоличествоСтрок, ФорматированнаяСумма);
		
		ПараметрыСуммаПрописью = Новый Структура;
		ПараметрыСуммаПрописью.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		ПараметрыСуммаПрописью.Вставить("СуммаПрописью" , РаботаСКурсамиВалют.СформироватьСуммуПрописью(ПараметрыИтого.Сумма, ВалютаПечати));
		
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "СуммаПрописью", ПараметрыСуммаПрописью);
		
		// Вывод области Подписи
		ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Подписи", ПараметрыИзШапки);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьПараметрыШапкиВедомостьНаСписание(Шапка)
	
	КлючиПараметров = ФормированиеПечатныхФормПоддержкаПроектов.ПолучитьИменаКолонокТаблицы(Шапка);
	
	Параметры = Новый Структура(КлючиПараметров);
	ЗаполнитьЗначенияСвойств(Параметры, Шапка);
	
	// Данные заголовка
	НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента);
	ШаблонТекстЗаголовка = НСтр("ru = 'Выбытие товаров № %1 от %2'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонТекстЗаголовка, НомерДокумента, Формат(Шапка.ДатаДокумента, "ДЛФ=DD"));
	
	СведенияОбОрганизации    = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);
	ОрганизацияПредставление = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,");
	СведенияОПолучателе      = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Шапка.Контрагент, Шапка.ДатаДокумента);
	ПолучательПредставление  = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПолучателе, "ПолноеНаименование,");
	
	Параметры.Вставить("ТекстЗаголовка"          , ТекстЗаголовка);
	Параметры.Вставить("ОрганизацияПредставление", ОрганизацияПредставление);
	Параметры.Вставить("ПолучательПредставление" , ПолучательПредставление);
	
	// Данные шапки таблицы
	Параметры.Вставить("ИмяКолонкиКодов", НСтр("ru = 'Код'"));
	
	// Данные подписей документа
	МОЛ = РегистрыСведений.МатериальноОтветственныеЛица.ПолучитьДанныеОтветственного(Шапка.Склад, Шапка.ДатаДокумента);
	
	Параметры.Вставить("Отпустил", МОЛ.ФИО);
	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьДанныеДляПечати(МассивОбъектов, ПараметрыПечати = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеДляПечати();
	Запрос.УстановитьПараметр("ТекущийДокумент", МассивОбъектов);
	ЗапасыСервер.УстановитьСтатусыПараметровУчетаВПараметрахЗапроса(Запрос);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке"          , РезультатыЗапросов[РезультатыЗапросов.ВГраница() - 1]);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти" , РезультатыЗапросов[РезультатыЗапросов.ВГраница()]);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

Функция ТекстЗапросаДанныеДляПечати()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Документ.Ссылка КАК Ссылка,
	               |	Документ.Номер КАК НомерДокумента,
	               |	Документ.Дата КАК ДатаДокумента,
	               |	Документ.Организация КАК Организация,
	               |	Документ.Организация КАК Отправитель,
	               |	Документ.ПодразделениеОрганизации КАК Подразделение,
	               |	Документ.ПодразделениеОрганизации.Представление КАК ПодразделениеПредставление,
	               |	Документ.Склад КАК Склад,
	               |	Документ.Склад.Представление КАК СкладПредставление,
	               |	Документ.Склад КАК СкладОтправитель,
	               |	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладПолучатель,
	               |	Документ.Контрагент КАК Контрагент,
	               |	Документ.Контрагент КАК Получатель,
	               |	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Перевозчик,
	               |	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК Исполнитель,
	               |	"""" КАК ИсполнительДолжность,
	               |	Документ.Валюта КАК Валюта,
	               |	Документ.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	               |	Документ.Центр.ДиректорДляПечати КАК ДиректорЦентра,
	               |	Документ.Центр.ПолноеНаименование КАК НаименованиеЦентра,
	               |	Документ.ПроектЗадания КАК ПроектЗадания,
	               |	Документ.Печать_НомерАкта КАК НомерАкта,
	               |	Документ.Печать_ДатаАкта КАК ДатаАкта,
	               |	Документ.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	Документ.Контрагент.НаименованиеПолное КАК ПолучательНаименованиеПолное
	               |ИЗ
	               |	Документ.ПередачаТоваровНаСторону КАК Документ
	               |ГДЕ
	               |	Документ.Ссылка В(&ТекущийДокумент)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Товары.Ссылка КАК Документ,
	               |	Товары.НомерСтроки КАК НомерСтроки,
	               |	Товары.Номенклатура КАК Номенклатура,
	               |	Товары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	               |	Товары.Номенклатура.Описание КАК Описание,
	               |	Товары.Номенклатура.Код КАК ТоварКод,
	               |	Товары.СерияНоменклатуры КАК СерияНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияСерий В (&СтатусУчетСебестоимостиПоСериям)
	               |			ТОГДА Товары.СерияНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК СерияНоменклатурыДляСебестоимости,
	               |	Товары.Партия КАК Партия,
	               |	ВЫБОР
	               |		КОГДА Товары.СтатусУказанияПартий В (&СтатусУчетСебестоимостиПоПартиям)
	               |			ТОГДА Товары.Партия
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ПартияДляСебестоимости,
	               |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Товары.ЕдиницаИзмерения.КодОКЕИ КАК КодПоОКЕИ,
	               |	Товары.Количество КАК Количество,
	               |	Товары.Количество КАК КОтгрузке,
	               |	Товары.Количество КАК Отпущено,
	               |	Товары.Количество КАК КоличествоБазовых,
	               |	Товары.Цена КАК Цена,
	               |	Товары.Сумма КАК Сумма,
	               |	Товары.СтавкаНДС КАК СтавкаНДС,
	               |	Товары.СуммаНДС КАК СуммаНДС,
	               |	Товары.СуммаСНДС КАК СуммаСНДС,
	               |	"""" КАК Основание
	               |ИЗ
	               |	Документ.ПередачаТоваровНаСторону.Товары КАК Товары
	               |ГДЕ
	               |	Товары.Ссылка В(&ТекущийДокумент)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Документ,
	               |	НомерСтроки
	               |ИТОГИ ПО
	               |	Документ";
	
	Возврат ТекстЗапроса;
	
КонецФункции


#Область ПечатьАктСписания

Функция ПечатьАктСписания(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	
	ПолноеИмяМакета = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(ПустаяСсылка().Метаданные().Макеты.ПФ_MXL_АктСписанияНаСторону);
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_" + ПолноеИмяМакета;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПолноеИмяМакета);
	
	МассивВыводимыхОбластей = Новый Массив();
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ПолучатьЦены");
	ДанныеДляПечати = ПолучитьДанныеДляПечати(МассивОбъектов, ПараметрыПечати);
	
	Шапка = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		     				
			ПервыйДокумент = Ложь;
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(Шапка.Ссылка, ТабличныйДокумент, Макет);
			
			// Получение параметров для заполнения
			ПараметрыИзШапки = ПолучитьПараметрыШапкиАктСписания(Шапка);
						
			// Вывод области Шапка
			ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Шапка", ПараметрыИзШапки);
			
			ВыборкаПоДокументам.Сбросить();
			ПараметрыПоиска = Новый Структура("Документ", Шапка.Ссылка);
			Если ВыборкаПоДокументам.НайтиСледующий(ПараметрыПоиска) Тогда
				ВыборкаСтрокТовары = ВыборкаПоДокументам.Выбрать();
				
				КоличествоСтрок = ВыборкаСтрокТовары.Количество();
				
				НомерСтроки = 0;
				Количество = 0;
				Пока ВыборкаСтрокТовары.Следующий() Цикл
					//Для Каждого ВыборкаСтрокТовары Из ВыгрузкаСтрокТовары Цикл
					
					НомерСтроки = НомерСтроки + 1;
					Количество = Количество + ВыборкаСтрокТовары.Количество;
					ДанныеСтроки = Новый Структура("НомерПП, Номенклатура, Производитель, Серия, Количество, Основание", НомерСтроки, ВыборкаСтрокТовары.Номенклатура, ВыборкаСтрокТовары.Номенклатура.Производитель, ВыборкаСтрокТовары.СерияНоменклатуры, ВыборкаСтрокТовары.Количество, ВыборкаСтрокТовары.Основание);
					ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
					ОбластьСтрока.Параметры.Заполнить(ДанныеСтроки);
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, МассивВыводимыхОбластей) Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					
					// Вывод области Строка
					ТабличныйДокумент.Вывести(ОбластьСтрока);
					
					//КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
			ПараметрыИзШапки.Вставить("КоличествоУпаковок", КоличествоСтрок);
			ПараметрыИзШапки.Вставить("Количество", Количество);
			
			// Вывод области Подписи
			ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Подвал", ПараметрыИзШапки);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
				
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьПараметрыШапкиАктСписания(Шапка)
	
	СведенияОбОрганизации    = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента);
	ОрганизацияПредставление = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации);
	
	ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Шапка.Организация, Шапка.ДатаДокумента);
	МОЛСдал = РегистрыСведений.МатериальноОтветственныеЛица.ПолучитьДанныеОтветственного(Шапка.Склад, Шапка.ДатаДокумента);
	
	Параметры = Новый Структура();
	
	Параметры.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента));
	Параметры.Вставить("ДатаДокумента", Формат(Шапка.ДатаДокумента, "ДЛФ=Д"));
	Параметры.Вставить("НомерРасписки", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента));
	Параметры.Вставить("ДатаРасписки", Формат(Шапка.ДатаДокумента, "ДЛФ=Д"));
	Параметры.Вставить("НомерЗадания", ?(Шапка.Ссылка.ПроектЗадания = Неопределено,"",Шапка.Ссылка.ПроектЗадания.НомерПроекта));
	Параметры.Вставить("Гриф", Шапка.Ссылка.Гриф.ПолноеНаименование);
	Параметры.Вставить("ДиректорЦентра", Шапка.ДиректорЦентра);
	Параметры.Вставить("НаименованиеЦентра", Шапка.НаименованиеЦентра);
	Параметры.Вставить("РуководительФИО"         , ОтветственныеЛица.РуководительНаименование);
	Параметры.Вставить("РуководительДолжность"   , ОтветственныеЛица.РуководительДолжность);
	Параметры.Вставить("НомерАктаВП", Шапка.НомерАкта);
	Параметры.Вставить("ДатаАктаВП", Формат(Шапка.ДатаАкта,"ДФ=dd.MM.yyyy"));
	
	Параметры.Вставить("ПредставлениеОрганизации", Шапка.ОрганизацияНаименованиеПолное);
	Параметры.Вставить("ПредприятиеПолучатель", Шапка.ПолучательНаименованиеПолное);
	
	Параметры.Вставить("СкладОтправительДолжностьОтветственного", МОЛСдал.Должность);
	
	//Параметры.Вставить("СкладПолучательДолжностьОтветственного", МОЛПринял.Должность);
	
	Параметры.Вставить("СкладОтправительОтветственный", ?(МОЛСдал.Ответственный.Пустая(), "",
		ФизическиеЛицаКлиентСервер.ФамилияИнициалы(МОЛСдал.Ответственный)));
	
	//Параметры.Вставить("СкладПолучательОтветственный", ?(МОЛПринял.Ответственный.Пустая(), "",
	//	ФизическиеЛицаКлиентСервер.ФамилияИнициалы(МОЛПринял.Ответственный)));
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти // ПечатьАктСписания

#Область ПечатьОписьКАктуСписания

Функция ПечатьОписьКАктуСписания(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	
	ПолноеИмяМакета = ФормированиеПечатныхФормПоддержкаПроектов.ПутьКМакету(ПустаяСсылка().Метаданные().Макеты.ПФ_MXL_ОписьКАктуСписанияНаСторону);
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_" + ПолноеИмяМакета;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПолноеИмяМакета);
	
	МассивВыводимыхОбластей = Новый Массив();
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ПолучатьЦены");
	ДанныеДляПечати = ПолучитьДанныеДляПечати(МассивОбъектов, ПараметрыПечати);
	
	Шапка = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Для Экз = 1 По 4 Цикл
			
			Если Не Экз = 1 Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
		
			ПервыйДокумент = Ложь;
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(Шапка.Ссылка, ТабличныйДокумент, Макет);
			
			// Получение параметров для заполнения
			ПараметрыИзШапки = ПолучитьПараметрыШапкиОписьКАктуСписания(Шапка);
			ПараметрыИзШапки.Вставить("Экземпляр", Экз);
			
			// Вывод области Шапка
			ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Шапка", ПараметрыИзШапки);
			
			ВыборкаПоДокументам.Сбросить();
			ПараметрыПоиска = Новый Структура("Документ", Шапка.Ссылка);
			Если ВыборкаПоДокументам.НайтиСледующий(ПараметрыПоиска) Тогда
				ВыборкаСтрокТовары = ВыборкаПоДокументам.Выбрать();
				КоличествоСтрок = ВыборкаСтрокТовары.Количество();
				
				НомерСтроки = 0;
				Количество = 0;
				
				Пока ВыборкаСтрокТовары.Следующий() Цикл
					//Для Каждого ВыборкаСтрокТовары Из ВыгрузкаСтрокТовары Цикл
					
					НомерСтроки = НомерСтроки + 1;
					Количество = Количество + ВыборкаСтрокТовары.Количество;
					ДанныеСтроки = Новый Структура("НомерПП, НомерУпаковки, Номенклатура, Производитель, Серия, Количество", Строка(НомерСтроки) + ".1", НомерСтроки, ВыборкаСтрокТовары.Номенклатура, ВыборкаСтрокТовары.Номенклатура.Производитель, ВыборкаСтрокТовары.СерияНоменклатуры, ВыборкаСтрокТовары.Количество);
					ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
					ОбластьСтрока.Параметры.Заполнить(ДанныеСтроки);
					
					ОбластьСтрокаУпаковка = Макет.ПолучитьОбласть("СтрокаУпаковка");
					ОбластьСтрокаУпаковка.Параметры.Заполнить(ДанныеСтроки);
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, МассивВыводимыхОбластей) Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					
					// Вывод области Строка
					ТабличныйДокумент.Вывести(ОбластьСтрокаУпаковка);
					ТабличныйДокумент.Вывести(ОбластьСтрока);
					
					//КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
			ПараметрыИзШапки.Вставить("КоличествоУпаковок", КоличествоСтрок);
			ПараметрыИзШапки.Вставить("Количество", Количество);
			
			// Вывод области Подписи
			ФормированиеПечатныхФормПоддержкаПроектов.ВывестиОбластьПоИмени(ТабличныйДокумент, Макет, "Подвал", ПараметрыИзШапки);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьПараметрыШапкиОписьКАктуСписания(Шапка)
	
	Параметры = Новый Структура();
	
	Параметры.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента));
	Параметры.Вставить("ДатаДокумента", Формат(Шапка.ДатаДокумента, "ДЛФ=Д"));
	Параметры.Вставить("ДО", Шапка.Ссылка.ПроектЗадания.ДобывающаяОрганизация);
	Параметры.Вставить("НомерЗадания", Шапка.Ссылка.ПроектЗадания.НомерПроекта);
	Параметры.Вставить("Гриф", Шапка.Ссылка.Гриф);
	Параметры.Вставить("НаименованиеЦентра", Шапка.НаименованиеЦентра);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти // ПечатьОписьКактуПриемки


#КонецОбласти // Печать

////////////////////////////////////////////////////////////////////////////////
// Команды формы
#Область КомандыФормы

// Заполняет список команд ввода на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыСоздатьНаОсновании(КомандыСоздатьНаОсновании, НастройкиФормы) Экспорт
	
	ВводНаОснованииПоддержкаПроектов.ДобавитьКомандыСоздатьНаОсновании(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыСоздатьНаОсновании, НастройкиФормы);
	
КонецПроцедуры

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, НастройкиФормы) Экспорт
	
	МенюОтчетыПоддержкаПроектов.ДобавитьОбщиеКоманды(ПустаяСсылка().Метаданные().ПолноеИмя(), КомандыОтчетов, НастройкиФормы);
	
КонецПроцедуры

#КонецОбласти // КомандыФормы

////////////////////////////////////////////////////////////////////////////////
// Прочее
#Область Прочее

Функция ЭтоБезвозмезднаяПередача(ХозяйственнаяОперация) Экспорт
	
	БезвозмезднаяПередача = Новый Массив;
	БезвозмезднаяПередача.Добавить(Перечисления.ХозяйственныеОперации.ВнутриведомственноеПеремещение);
	БезвозмезднаяПередача.Добавить(Перечисления.ХозяйственныеОперации.БезвозмезднаяПередачаМежбюджетная);
	БезвозмезднаяПередача.Добавить(Перечисления.ХозяйственныеОперации.БезвозмезднаяПередачаПрочее);
	
	Возврат (БезвозмезднаяПередача.Найти(ХозяйственнаяОперация) <> Неопределено);
	
КонецФункции

#КонецОбласти // Прочее

#КонецОбласти // СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СТАНДАРТНЫЕ ПОДСИСТЕМЫ
#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти // СтандартныеПодсистемы

#КонецЕсли