
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Добавляет в список команды отчетов по умолчанию
//
// Параметры:
//  ПолноеИмяОбъекта - Строка - полное имя объекта для которого нужно добавить команды отчетов.
//  КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Процедура ДобавитьОбщиеКоманды(ПолноеИмяОбъекта, КомандыОтчетов, НастройкиФормы) Экспорт
	
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъекта);
	Если Метаданные.КритерииОтбора.СвязанныеДокументы.Тип.СодержитТип(ТипЗнч(Менеджер.ПустаяСсылка())) Тогда
		ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта) И МетаданныеОбъекта.Движения.Количество() > 0 Тогда
		ДобавитьКомандуДвиженияДокумента(КомандыОтчетов)
	КонецЕсли;
	
КонецПроцедуры

// Добавляет команду отчета в список команд.
// 
// Параметры:
//  КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Функция ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.СтруктураПодчиненности) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		КомандаОтчет.ИмяФормы = Метаданные.ОбщиеФормы.СтруктураПодчиненности.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Структура подчиненности'");
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = ПодменюОтчетыСмТакже();
		КомандаОтчет.Картинка      = БиблиотекаКартинок.СтруктураПодчиненности;
		КомандаОтчет.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.S, Ложь, Истина, Истина); // Ctrl+Shift+S
		КомандаОтчет.ИмяПараметраФормы = "ОбъектОтбора";
		
		КомандаОтчет.Порядок = 100;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//  КомандыОтчетов - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//
Функция ДобавитьКомандуДвиженияДокумента(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДвиженияДокумента) Тогда
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Менеджер"    , Метаданные.Отчеты.ДвиженияДокумента.ПолноеИмя());
		СтруктураПоиска.Вставить("ТипПараметра", Неопределено);
		НайденныеСтроки = КомандыОтчетов.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Команда = НайденныеСтроки[0];
		Иначе
			Команда = КомандыОтчетов.Добавить();
			Команда.МножественныйВыбор = Ложь;
			Команда.ИмяПараметраФормы  = "";
			Команда.КлючВарианта       = "Основной";
			Команда.Менеджер           = Метаданные.Отчеты.ДвиженияДокумента.ПолноеИмя();
		КонецЕсли;
		Команда.Представление = НСтр("ru = 'Движения документа'");
		Команда.Важность      = ПодменюОтчетыСмТакже();
		Команда.Картинка      = БиблиотекаКартинок.ДебетКредит;
		Команда.Порядок       = 100;
		Команда.Обработана    = Ложь;
		
		Возврат Команда;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает идентификатор подменю "Отчеты->Важное"
//
Функция ПодменюОтчетыВажное() Экспорт
	Возврат "Важное";
КонецФункции

// Возвращает идентификатор подменю "Отчеты->Обычное"
//
Функция ПодменюОтчетыОбычное() Экспорт
	Возврат "Обычное";
КонецФункции

// Возвращает идентификатор подменю "Отчеты->См.также"
//
Функция ПодменюОтчетыСмТакже() Экспорт
	Возврат "СмТакже";
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
