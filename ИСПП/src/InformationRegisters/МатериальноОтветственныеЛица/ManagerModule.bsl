#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Получить данные ответственного.
// 
// Параметры:
//  Склад - СправочникСсылка.Склады
//  Период - Дата
// 
// Возвращаемое значение:
//  Структура - Получить данные ответственного:
// * Ответственный - СправочникСсылка.ФизическиеЛица - 
// * ФИО - Строка - 
// * Должность - Строка - 
Функция ПолучитьДанныеОтветственного(Склад, Период) Экспорт
	
	ДанныеОтветственного = Новый Структура;
	ДанныеОтветственного.Вставить("Ответственный", Справочники.ФизическиеЛица.ПустаяСсылка());
	ДанныеОтветственного.Вставить("ФИО","");
	ДанныеОтветственного.Вставить("Должность", "");
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ответственный КАК Ответственный,
	|	Должность КАК Должность
	|ИЗ
	|	РегистрСведений.МатериальноОтветственныеЛица.СрезПоследних(&Период, Склад = &Склад)
	|");
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ДанныеОтветственного, Выборка);
		ДанныеОтветственного.ФИО = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СокрЛП(ДанныеОтветственного.Ответственный));
	КонецЕсли;
	
	Возврат ДанныеОтветственного;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли