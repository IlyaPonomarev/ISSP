#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
#Область ПрограммныйИнтерфейс

// Процедура сообщает пользователю об ошибках проведения по регистру СебестоимостьТоваров
//
// Параметры
//	Объект - проводимый документ
//	Отказ - признак отказа от проведения документа
//	РезультатЗапроса - информация об ошибках проведения по регистру
//
Процедура СообщитьОбОшибкахПроведения(Объект, Отказ, РезультатЗапроса) Экспорт
	
	ШаблонОперативный = НСтр("ru = 'Номенклатура %Номенклатура% 
		|Превышен оперативный остаток товара по организации %Организация% на складе %Склад% на %Количество% (%Единица%)'");
	ШаблонКонтрольный = НСтр("ru = 'Номенклатура %Номенклатура% 
		|Превышен остаток товара на %ДатаОстатка% по организации %Организация% на складе %Склад% на %Количество% (%Единица%)'");
	
	ПредыдущаяЗапись = Новый Структура("АналитикаВидаУчета, АналитикаУчетаНоменклатуры");
	
	ПроводитьБезКонтроляОстатков = ПараметрыСеанса.ПроводитьБезКонтроляОстатковТоваровПоРегиструСебестоимости;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ПредыдущаяЗапись.АналитикаВидаУчета <> Выборка.АналитикаВидаУчета
		 Или ПредыдущаяЗапись.АналитикаУчетаНоменклатуры <> Выборка.АналитикаУчетаНоменклатуры Тогда
			ЗаполнитьЗначенияСвойств(ПредыдущаяЗапись, Выборка);
		Иначе
			Продолжить;
		КонецЕсли;
		ШаблонСообщения = ?(Выборка.ДатаОстатка = ПроведениеПоддержкаПроектов.ДатаАктуальныхОстатков(), ШаблонОперативный, ШаблонКонтрольный);
		
		ТекстСообщения = СтрЗаменить(
			ШаблонСообщения, "%Номенклатура%",
			ОбщегоНазначенияПоддержкаПроектов.ПолучитьПредставлениеНоменклатуры(Выборка.Номенклатура, Выборка.СерияНоменклатуры, Выборка.Партия));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДатаОстатка%", Формат(Выборка.ДатаОстатка, "ДЛФ=D"));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Организация%", Строка(Выборка.Организация));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Склад%",Строка(Выборка.Склад));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Количество%", Строка(-Выборка.Количество));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Единица%", Строка(Выборка.Упаковка));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект);
		
		Если Не ПроводитьБезКонтроляОстатков Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПроводитьБезКонтроляОстатков И Не РезультатЗапроса.Пустой() Тогда
		
		ТекстСообщения = НСтр( "ru = 'Документ %Ссылка% проведен с выключенным контролем остатков товаров по регистру себестоимости.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Объект.Ссылка);
		
		ЗаписьЖурналаРегистрации(ЗапасыСервер.ИмяСобытияВыключенКонтрольОстатков(),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			Объект.Ссылка,
			ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли