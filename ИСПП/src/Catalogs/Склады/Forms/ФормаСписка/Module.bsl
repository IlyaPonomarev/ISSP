
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтчетПоСкладам(Команда)
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
        Возврат;
    КонецЕсли;	

	//@skip-warning
	ПользовательскиеНастройки = ПолучитьНастройкиПоУмолчанию("ВедомостьПоТоварамНаСкладах");
    Отбор = Новый Структура("Склад", Элементы.Список.ТекущаяСтрока);

	//@skip-check structure-consructor-too-many-keys
	ПараметрыОтчета = Новый Структура("СформироватьПриОткрытии, КлючВарианта, Отбор, ПользовательскиеНастройки", Истина, "Остатки", Отбор, ПользовательскиеНастройки);
	ОткрытьФорму("Отчет.ВедомостьПоТоварамНаСкладах.Форма", ПараметрыОтчета);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ПолучитьНастройкиПоУмолчанию(ТипОтчета)
	СхемаКомпоновкиДанных = Отчеты[ТипОтчета].ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");

    КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных();
    КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
    КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	Возврат КомпоновщикНастроекКомпоновкиДанных.ПользовательскиеНастройки;
КонецФункции
#КонецОбласти
