
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = Параметры.Номенклатура;
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "ВидНоменклатуры");	
	
	ВидНоменклатуры = РеквизитыНоменклатуры.ВидНоменклатуры;
	УстановитьНастройкиПоВидуНоменклатуры();
	
	ПараметрыЗапросаИзПараметровФормы = Новый Структура;
	ПараметрыЗапросаИзПараметровФормы.Вставить("Номенклатура", Параметры.Номенклатура);
	ПараметрыЗапросаИзПараметровФормы.Вставить("Организация", Параметры.Организация);
	ПараметрыЗапросаИзПараметровФормы.Вставить("Склад", Параметры.Склад);
	ПараметрыЗапросаИзПараметровФормы.Вставить("Регистратор", Параметры.Регистратор);
	
	ПараметрыЗапроса = Новый ФиксированнаяСтруктура(ПараметрыЗапросаИзПараметровФормы);
	
	ЗаполнитьОстаткиСерий();
	
	Если ЗначениеЗаполнено(Параметры.Текст) Тогда
		РеквизитыСерии = РеквизитыСерииИзСтроки(Параметры.Текст, ПараметрыСерий);
	ИначеЕсли ЗначениеЗаполнено(Параметры.Серия) Тогда
		РеквизитыСерии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Серия, "КодГода, КодЦентра, КодДО, КодГрифа, УчетныйНомер");
	КонецЕсли;
	
	Если РеквизитыСерии <> Неопределено Тогда
		КодГода = РеквизитыСерии.КодГода;
		КодЦентра = РеквизитыСерии.КодЦентра;
		КодДО   = РеквизитыСерии.КодДО;
		КодГрифа =  РеквизитыСерии.КодГрифа;
		УчетныйНомер = РеквизитыСерии.УчетныйНомер;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтрДлина(УчетныйНомер) > 0 Тогда
		Элементы.Номер.УстановитьГраницыВыделения(СтрДлина(УчетныйНомер),СтрДлина(УчетныйНомер));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Элементы.ГруппаСтраницыСерий.ТекущаяСтраница = Элементы.ГруппаВводСерии Тогда
		ЗавершитьВводСерии();
	Иначе
		ВыбратьСерию();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОстаткиСерийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьСерию();
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершитьВводСерии()
	
	Если ПроверитьЗаполнение() Тогда
		
		Серия = НайтиСоздатьСерию();
		Если Не ЗначениеЗаполнено(Серия) Тогда
			Возврат;
		КонецЕсли;
		
		ОповеститьОВыборе(Серия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиСоздатьСерию()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СерииНоменклатуры.Ссылка КАК Серия
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.Владелец = &ВидНоменклатуры
	|	И СерииНоменклатуры.КодГода = &КодГода
	|	И СерииНоменклатуры.КодЦентра = &КодЦентра
	|	И СерииНоменклатуры.КодДО = &КодДО
	|	И СерииНоменклатуры.КодГрифа = &КодГрифа
	|	И СерииНоменклатуры.УчетныйНомер = &УчетныйНомер";
	
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);
	
	Если ПараметрыСерий.ИспользоватьНомерСерии Тогда
		Запрос.УстановитьПараметр("КодГода", Прав(Формат(Год(Дата),"ЧГ"), 2));
		Запрос.УстановитьПараметр("КодЦентра", Центр.Код);
		Запрос.УстановитьПараметр("КодДО", ДО.Код);
		Запрос.УстановитьПараметр("КодГрифа", Гриф.Код);
		Запрос.УстановитьПараметр("ВидНоменклатуры", СокрЛП(УчетныйНомер));
	Иначе
		Запрос.УстановитьПараметр("КодГода", 0);
		Запрос.УстановитьПараметр("КодЦентра", 0);
		Запрос.УстановитьПараметр("КодДО", 0);
		Запрос.УстановитьПараметр("КодГрифа", 0);
		Запрос.УстановитьПараметр("ВидНоменклатуры", "");
	КонецЕсли;	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Серия;
	Иначе
		
		Если Не ПравоДоступа("Добавление", Метаданные.Справочники.СерииНоменклатуры) Тогда
			
			ТекстСообщения = НСтр("ru = 'В программе еще не зарегистрирована серия %ПредставлениеСерии%. Недостаточно прав для регистрации новой серии.'");
			
			ПредставлениеСерии = Справочники.СерииНоменклатуры.ПредставлениеСерии(ПараметрыСерий, Запрос.Параметры);
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредставлениеСерии%", ПредставлениеСерии);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат Справочники.СерииНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
		СерияОбъект = Справочники.СерииНоменклатуры.СоздатьЭлемент();
		СерияОбъект.Владелец = ВидНоменклатуры;
		СерияОбъект.КодГода = Прав(Формат(Год(Дата),"ЧГ"),2);
		СерияОбъект.КодЦентра = Центр.Код;
		СерияОбъект.КодДО = ДО.Код;
		СерияОбъект.КодГрифа = Гриф.Код;
		СерияОбъект.УчетныйНомер = СокрЛП(УчетныйНомер);
		
		СерияОбъект.Записать();
		
		Возврат СерияОбъект.Ссылка;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьСерию()

	ТекущиеДанные = Элементы.ОстаткиСерий.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Выберите серию или введите новую.'");	
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	Иначе	
		Серия = ТекущиеДанные.Серия;
	КонецЕсли;
	
	ОповеститьОВыборе(Серия);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиПоВидуНоменклатуры()
	
	Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСерий = ЗначениеНастроекПоддержкаПроектовПовтИсп.ПараметрыСерийНоменклатуры(ВидНоменклатуры);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыСерииИзСтроки(Знач Строка, ПараметрыШаблона)
	
	РеквизитыСерии = Новый Структура("КодГода, КодЦентра, КодДО, КодГрифа, УчетныйНомер, ЗаводскойНомер", 0, 0, 0, 0, "", "");
	КодГода = Неопределено;
	КодЦентра = Неопределено;
	КодДО = Неопределено;
	КодГрифа = Неопределено;
	УчетныйНомер = Неопределено;
	ЗаводскойНомер = Неопределено;
	
	Если ПараметрыШаблона.ИспользоватьНомерСерии Тогда
		Номер = Строка;
	КонецЕсли;
	
	Если Номер <> Неопределено Тогда
		РеквизитыСерии.Номер = Номер;
	КонецЕсли;
	
	Возврат РеквизитыСерии;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОстаткиСерий()

	Если ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыНаСкладах) Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	СерияНоменклатуры КАК Серия,
		|	0 КАК Остаток
		|ПОМЕСТИТЬ ДанныеРегистров
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах КАК Товары
		|ГДЕ
		|	Номенклатура = &Номенклатура
		|	И СерияНоменклатуры <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|";
		
	Иначе
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
		|	0 КАК Остаток
		|ПОМЕСТИТЬ ДанныеРегистров
		|ГДЕ
		|	ЛОЖЬ
		|";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистров.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДанныеРегистров.Серия КАК Справочник.СерииНоменклатуры).КодГода КАК КодГода,
	|	ВЫРАЗИТЬ(ДанныеРегистров.Серия КАК Справочник.СерииНоменклатуры).КодЦентра КАК КодЦентра,
	|	ВЫРАЗИТЬ(ДанныеРегистров.Серия КАК Справочник.СерииНоменклатуры).КодДО КАК КодДО,
	|	ВЫРАЗИТЬ(ДанныеРегистров.Серия КАК Справочник.СерииНоменклатуры).КодГрифа КАК КодГрифа,
	|	ВЫРАЗИТЬ(ДанныеРегистров.Серия КАК Справочник.СерииНоменклатуры).УчетныйНомер КАК УчетныйНомер,
	|	ДанныеРегистров.Остаток КАК Остаток
	|ИЗ
	|	ДанныеРегистров КАК ДанныеРегистров
	|ГДЕ
	|	ДанныеРегистров.Остаток > 0
	|	ИЛИ &ВсеСерии
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодГода,
	|	КодЦентра,
	|	КодДО,
	|	КодГрифа,
	|	УчетныйНомер";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ВсеСерии", Истина);
	Для Каждого Параметр Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	ОстаткиСерий.Загрузить(Запрос.Выполнить().Выгрузить());
	
	КоличествоСерий = ОстаткиСерий.Количество();
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
